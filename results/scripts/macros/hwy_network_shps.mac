~# Exports highway network shapefiles for the peak and daily scenarios.
~#
~# Call syntax: hwy_network_shps.mac <1> <2>
~#   1 - Path to directory for output
~#   2 - 3-digit scenario number
~#
~/ Export AM peak highway network shapefile
1.22  /Scenario manipulations
3       /Copy a scenario
3         /Scenario to be copied
99        /Scenario to hold the copy
ampk 7-9am  /Title of scenario copy
y         /Set copy as the current scenario?
q
2.41  /Network calculations
1       /Network calculations and mode changes
y         /Save result?
ul1       /Attribute to save result in
~/        Expression to calculate result
0
 
*         /Selected link types or attributes= all
2         /Type of output to be generated= summary report
1         /List device= Terminal
 
~+;1;y;ul2;0; ;*;2;1; 
~+;1;y;ul3;0; ;*;2;1; 
~+;1;y;ul1
@avauv+@avh2v+@avh3v+@avbqv+@avlqv+(@avmqv/2)+(@avhqv/3)
~+; ;*;2;1; ;q
2.42  /Extra attribute manipulations
1       /Initialize extra attribute table
y         /All extra attributes will be deleted. OK?
q
~<emme2shp %1%\highway_ampk-%2%
~#
~/ Export PM peak highway network shapefile
1.22
2       /Delete a scenario
99        /Scenario to be deleted
y         /Scenario will be deleted. OK?
~+;3;7;99;pmpk 4-6pm;y
q
2.41
~+;1;y;ul1;0; ;*;2;1; 
~+;1;y;ul2;0; ;*;2;1; 
~+;1;y;ul3;0; ;*;2;1; 
~+;1;y;ul1
@avauv+@avh2v+@avh3v+@avbqv+@avlqv+(@avmqv/2)+(@avhqv/3)
~+; ;*;2;1; 
q
~+;2.42;1;y;q
~<emme2shp %1%\highway_pmpk-%2%
~+;1.22;2;99;y;q
~#
~/ Export daily highway network shapefile
s=%2%49
~<emme2shp %1%\highway-%2%
