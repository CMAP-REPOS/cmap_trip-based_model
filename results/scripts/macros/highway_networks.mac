~# Exports the highway network and attributes, as Emme batch files, for
~# each time of day period and daily scenario.
~#
~# Call syntax: highway_networks.mac <1> <2>
~#   1 - Path to directory for output
~#   2 - 3-digit scenario number
~#
~/Export highway network and attributes for each time of day period
~y=0
~:loop
~y+1
1.22  /Scenario manipulations
3       /Copy a scenario
%y%       /Scenario to be copied
99        /Scenario to hold the copy
Copy of p%y%  /Title of scenario copy
y         /Set copy as current scenario?
q
2.42  /Extra attribute manipulations
2       /Create attribute
2         /Attribute type= link
@vadt     /Attribute name
adt p%y%  /Description
0         /Default value
q
2.41  /Network calculations
1       /Network calculations and mode changes
y         /Save result?
@vadt     /Attribute to save result in
n         /Change description of attribute?
~#         Expression to calculate
@avauv+@avh2v+@avh3v+@avbqv+@avlqv+(@avmqv/2)+(@avhqv/3)
 
*         /Selected link types or attributes= all
2         /Type of output= summary report
1         /List device= Terminal
 
q
batchout=%1%/?
2.14  /Output base network
5       /Export base network
network_p%y%.txt
y
y
q
~+;2.41;1;n
@speed+@width+@parkl+@toll+@sigic+@tipid+@ftime+@emcap+@avelw+@vadt+timau
~+; ;*;3;attribs_p%y%.txt;q
1.22
2       /Delete a scenario
99        /Scenario to be deleted
y         /Scenario will be deleted. OK?
q
~?y<8
~$loop
~#
~/ Export daily highway network and attributes
s=%2%49
~+;2.14;5;network_daily.txt;y;y;q
~+;2.41;1;n;@vadt; ;*;3;attribs_daily.txt;q
