~/ INITIALIZE.SCENARIOS, kww 11/99, revised 5/05.
~#      Heither 01/06/09 - added line at end to overwrite bus.link file in database\data\ with new one.
~#      Heither 03/30/10 - added lines at end to copy M01 and DISTR files to appropriate location.
~#      Heither 09/14/10 - added line at end to copy MCHW_HH.TXT to appropriate location.
~#      Heither 04/15/11 - another program now copies M01/DISTR/MCHW_HH.TXT to appropriate location so 
~#                         coding from 03/30/10 & 09/14/10 updates removed.
~#      Heither 03/02/12 - dropped scenario %1%0 (copy of %1%00) because it serves no purpose.
~#      Heither 09/24/12 - copy linkshape file into data/.
~#      Heither 12/19/13 - points to new linkshape file location.
~#      Heither 09/15/14 - import linkshape information into highway scenarios.
~#      Ferguson 08/04/16 - now calls import.turn instead of import.dummy.turn to import turn prohibitions
~#      Heither 02/27/17 - implement vehicle class and time-of-day toll rates
~#	Bozic   03/23/2017 - implement bus veq as an extra highway attribute and set el2=@busveq
~#                           this means you should have the 8 period bus itineraries in the batchin file
~#                           REMOVE "INIT" FROM TRANSIT MODE BATCHIN FILE!
~#      Ferguson 8/20/2018: added @imarea to n2 batchin and updated toll rate definitions to use zone17
~#  =========================================================================================
~/  1. CREATE 8 SCENARIOS REQUIRED FOR REGIONAL ANALYSIS
~/  2. CREATE EXTRA ATTIBUTE TABLES 
~/  3. IMPORT 8 SETS OF HIGHWAY NETWORK DATA
~/
~/correct syntax is:
~/ prep_macros/initialize.scenarios <3 digit tod numeric> 
~#  =========================================================================================
~r11=%1%
~+;~?e;~$>error
~/
~# ################################
~# -- SET PATH TO BATCHIN FILES --
~t2=M:\proj1\nrf\Conformity\c23q2\
~t3=highway
~# ################################
~/ ## - batchin source = %t2%%t3% - ##
~#
~o|39
~t1=base daily network - all links 
~/
~/ CREATE SCENARIOS AND EXTRA ATTRIBUTES
~/
~r11*100
1.22
~+;1;%r11%;%t1%;y;q
~/
batchin=data/modes.in
2.01
~+;1;1;;q
1.23
~+;7;5;q
~/
2.42
~+;2;1;@zone;CMAP zone17;
~+;2;1;@atype;CMAP capacity zone;
~+;2;1;@imarea;IM area flag;
~+;2;2;@busveq;period bus veqs;
~+;2;2;@speed;posted speed;
~+;2;2;@width;average lane width;
~+;2;2;@parkl;number of park lanes;
~###+;2;2;@cltl;continous left turn present;
~+;2;2;@toll;auto toll (dollars);
~+;2;2;@toll2;light truck toll (dollars);
~+;2;2;@toll3;medium truck toll (dollars);
~+;2;2;@toll4;heavy truck toll (dollars);
~+;2;2;@sigic;signal interconnect present;
~/###+;2;2;@rrx;railroad grade crossing present;
~+;2;2;@tipid;most recently applied tip id;;q;
~/*****************************
~t1=period 1.  8 pm to  6 am
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 2.  6 am to  7 am
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 3.  7 am to  9 am
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 4.  9 am to 10 am
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 5. 10 am to  2 pm
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 6.  2 pm to  4 pm
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 7.  4 pm to  6 pm
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
~t1=period 8.  6 pm to  8 pm
~+;~r1=%1%;~r1*100;~r11+1;
1.22
~+;3;%r1%;%r11%;%t1%;y;q
~/****
batchin=batchin
~/        IMPORT NETWORKS AND EXTRA ATTRIBUTES;
~/
~x=0
~#
~# -- PARAMETERS (toll factors for truck classes) - Noel's logic from ABM (somewhat expanded)
~r2=3.00 / Light truck, day (p. 2-8)
~r3=4.50 / Medium truck, day (p. 2-8)
~r4=8.00 / Heavy truck, day (p. 2-8)
~r5=2.00 / Light truck, night (p. 1)
~r6=3.50 / Medium truck, night (p. 1)
~r7=6.00 / Heavy truck, night (p. 1)
~r8=3.40 / Skyway Medium truck, day (p. 2-8)
~r9=5.60 / Skyway Heavy truck, day (p. 2-8)
~r10=2.40 / Skyway Medium truck, night (p. 1)
~r11=4.00 / Skyway Heavy truck, night (p. 1)
~r12=3.30 / Indiana Tollway Medium truck, all periods
~r13=9.00 / Indiana Tollway Heavy truck, all periods
~#
~:period
~#
~r1=%1%0%x%
s=%r1%
batchin=%t2%%t3%/%1%/%r1%.n1
2.11
1

batchin=%t2%%t3%/%1%/%r1%.l1
2.11
1

batchin=%t2%%t3%/%1%/%r1%.n2
2.41
~+;3;@zone;n;all;1,2;n;y
~+;3;@atype;n;all;1,3;n;y
~+;3;@imarea;n;all;1,4;n;y;q
batchin=%t2%%t3%/%1%/%r1%.l2
2.41
~+;3;@speed;n;all;1,2,3;n;y
~+;3;@width;n;all;1,2,4;n;y
~+;3;@parkl;n;all;1,2,5;n;y
~/+;3;@cltl;n;all;1,2,6;n;y
~+;3;@toll;n;all;1,2,7;n;y
~+;3;@sigic;n;all;1,2,8;n;y
~/+;3;@rrx;n;all;1,2,9;n;y
~+;3;@tipid;n;all;1,2,10;n;y;q
~# -- set vehicle class & TOD toll values
2.41
~# -- ISTHA rates
~+|1|y|@toll2|n|~?x=1|@toll*%r5%|~?!x=1|@toll*%r2%||@zone=1,3247||4
~+|1|y|@toll3|n|~?x=1|@toll*%r6%|~?!x=1|@toll*%r3%||@zone=1,3247||4
~+|1|y|@toll4|n|~?x=1|@toll*%r7%|~?!x=1|@toll*%r4%||@zone=1,3247||4
~# -- Skyway rates
~+|1|y|@toll2|n|@toll||@zone=650|@zone=697,699||4
~+|1|y|@toll3|n|~?x=1|@toll*%r10%|~?!x=1|@toll*%r8%||@zone=650|@zone=697,699||4
~+|1|y|@toll4|n|~?x=1|@toll*%r11%|~?!x=1|@toll*%r9%||@zone=650|@zone=697,699||4
~# -- Indiana Tollway rates (no TOD pricing)
~+|1|y|@toll2|n|@toll||@zone=3248,3467||4
~+|1|y|@toll3|n|@toll*%r12%||@zone=3248,3467||4
~+|1|y|@toll4|n|@toll*%r13%||@zone=3248,3467||4
q

~/===THIS IS WHERE THE @BUSVEQ IS CALCULATED.  BATCH IN ITINERARIES, CALCULATE AND DELETE==
~/--------input bus itineraries to calculate bus vehicles for background traffic----
~#skip this if it is period 0
~?x=0
~$>skip_p0
~# Set transit period number
~+;~?x=1;~y=1
~+;~?x=2;~y=2
~+;~?x=3;~y=2
~+;~?x=4;~y=3
~+;~?x=5;~y=3
~+;~?x=6;~y=3
~+;~?x=7;~y=4
~+;~?x=8;~y=1
reports=report\add.background.bus.rpt
batchin=%t2%\transit\?
~/==
~/register holds the period number
~r14=%x%
~/==
~/add transit modes to hwy network
~/and add transit vehicles
2.01
~+;2;2;B;cta bus;1
~+;2;2;E;cta express;1
~+;2;2;P;pace regular;1
~+;2;2;Q;pace express;1
~+;2;2;L;pace local;1;q
2.02
~#
~# Old transit vehicles (C21Q4 and earlier)
~#~+;2;1;B;cta bus;100;50,100; ; ; ; ;3
~#~+;2;2;E;cta express;100;50,100; ; ; ; ;3
~#~+;2;3;P;pace reg;100;50,100; ; ; ; ;3
~#~+;2;4;Q;pace exp;100;50,100; ; ; ; ;3
~#~+;2;5;L;pace local;100;50,100; ; ; ; ;3;q
~#
~+;2;25;B;ctaloc30; ;23,35; ; ; ; ;2
~+;2;26;B;ctaloc40; ;39,60; ; ; ; ;3
~+;2;27;B;ctaloc60; ;58,93; ; ; ; ;4
~+;2;28;P;pacebus40; ;39,60; ; ; ; ;3
~+;2;29;Q;pacebus40; ;39,60; ; ; ; ;3
~+;2;30;L;pacebus40; ;39,60; ; ; ; ;3
~+;2;31;E;ctaexp30; ;23,35; ; ; ; ;2
~+;2;32;E;ctaexp40; ;39,60; ; ; ; ;3
~+;2;33;E;ctaexp60; ;58,93; ; ; ; ;4;q
~/==
~#add bus modes to the highway network first
2.41
~+;1;y;modes;1;PEBQL;!vdf=6; ;y;q
~#
~#batchin bus itineraries for the period
2.21
~+;%1%\bus.itinerary_%y%;2
~#calculate background bus veq - 3 veqs * hours in the period
2.41
1
y
~+;@busveq;n;(3*60/hdwy)*
((5*(%r14%==1))+(1*(%r14%==2||%r14%==4))+
(4*(%r14%==5))+(2*(%r14%==3||%r14%>=6)))

~+;4;*;*;2;2;q
~#NOW DELETE THE ITINERARIES, MODES AND VEHICLES FROM THE NETWORK
~+;2.22;2;y;q
2.41
~+;1;y;modes;2;PEBQL;*;y;q
~#delete transit modes and vehicles from the scenario
2.02
~#
~# Old transit vehicles (C21Q4 and earlier)
~#~+;3;1
~#~+;3;2
~#~+;3;3
~#~+;3;4
~#~+;3;5;q
~#
~+;3;25
~+;3;26
~+;3;27
~+;3;28
~+;3;29
~+;3;30
~+;3;31
~+;3;32
~+;3;33;q
2.01
~+;3;B
~+;3;E
~+;3;P
~+;3;Q
~+;3;L;q
~:skip_p0
~/
el2=@busveq
~/==============================================================================================
~/------------------------------------------------------
~<prep_macros/call/import.turn
~/
~<prep_macros/call/Ftime.Capacity
~<prep_macros/call/Arterial.Delay
~/------------------------------
~/
~#
~#
~# ## Turn this off and just apply to final networks in 
~# ## -- Import Shape Points -- ##
~# ~t4=report\scen_%s%_highway_linkshape.rpt
~# ~!if exist %t4% (del %t4%)
~# ~!emmelinkshp -i %t2%linkshape\linkshape_%1%.in >> %t4%
~#
~#
~x+1
~+;~?x<9;~$period
~/******************************************************************
~/
s=%1%00
~/
~o=6
~!copy %t2%%t3%\%1%\bus.link data\bus.link /y
~!copy %t2%linkshape\linkshape_%1%.in data\linkshape_%1%.in /y
~/
~/ Done!
~$>end
~:error
~/
~/ERROR ERROR ERROR ERROR ERROR ERROR
~/
~/improper execution syntax
~/correct syntax is:
~/ prep_macros/initialize.scenarios <4 digit tod numeric> <run letter>
~/
~/ERROR ERROR ERROR ERROR ERROR ERROR
~:end
~/*****
batchin=
~/ -- end of macro --

