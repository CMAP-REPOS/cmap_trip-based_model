
~# BUILD_TOD_TRANSIT
~#   Craig Heither, 07-13-2022
~#
~#  ************************************************************************************************
~#    Macro used to create AM Peak and Midday transit networks for skimming to provide transit 
~#    information to the destination-mode choice model.
~#
~#    For AM Peak network: segment variable @hwytm is populated with data from file: data\per3.timau.
~#
~#    Must provide 3-digit scenario when submitting
~#      (e.g. " ~<prep_macros\build_tod_transit 100 ")
~#
~#  ************************************************************************************************
~# 11/08/2022 Ferguson: Commented out 17120001_parking.csv and cermakbrt_parking.csv batchin blocks.
~# =================================================================================================
~z=%0%
~+;~?!z=1;~$>error
~z=%2%
~/
~# ################################
~# -- SET PATH TO BATCHIN FILES --
~t2=M:\proj1\nrf\Conformity\c23q2\
~t7=transit
~# ################################
~/ ## - batchin source  = %t2%%t7% - ##
~#
~#
~o|39
~/ -- SET VARIABLES --
on=25                       /set to 4-digit date
~x=%1%
~t1=2019
~+;~?x=200;~t1=2025
~+;~?x=300;~t1=2030
~+;~?x=400;~t1=2035
~+;~?x=500;~t1=2040
~+;~?x=600;~t1=2045
~+;~?x=700;~t1=2050
~t4=am (6am-9am)
~t5=transit skim network - %d%
~t6=%t1%
~#
~y=0  / loop counter
~z=2  / transit TOD period
~#
~:loop
~+;~?y=5;~t4=midday (9am-4pm)
~#
~/ -- BUILD TRANSIT NETWORK %z% --
~+;~x=%1%;~x+%y%
s=%x%
~+;~?e;~$>new
~+;1.22; 2; %x%; y; q
~:new
~+;1.22; 1; %x%; %t1% %t4% %t5%; y; q
~#
~/ -- SET REPORT --
~t3=report\build_%s%transit.rpt
~!if exist %t3% (del %t3%)
reports=%t3%
~#
~/ -- Network --
batchin=%t2%%t7%\tranmodes.txt
~+;2.01; 1; 2; q
batchin=%t2%%t7%\transveh.txt
~+;2.02; 1; 2; q
batchin=%t2%%t7%\%1%\bus.network_%z%
~+;2.11; 2;
batchin=%t2%%t7%\%1%\rail.network_%z%
~+;2.11; 2;
batchin=%t2%%t7%\%1%\access.network_%z%
~+;2.11; 2;
~/ -- Transit Lines --
batchin=%t2%%t7%\%1%\rail.itinerary_%z%
~+;2.21; 2
batchin=%t2%%t7%\%1%\bus.itinerary_%z%
~+;2.21; 2
~/
~/ -- CREATE EXTRA ATTRIBUTES --
2.42
~+; 1; y
~+; 2; 1; @pcost; avg. daily parking cost at station; 0
~+; 2; 1; @atype; area type for on-street parking; 0
~+; 2; 1; @pspac; off-street parking spaces at station; 0
~+; 2; 1; @zone; CMAP zone; 0
~+; 2; 4; @ltime; line service time in minutes; 0
~+; 2; 4; @hwytm; per. 3 congested hwy time from %1%; 0
~+; 2; 4; @zfare; incremental zone fare; 0; q
~/
~/ -- SET EXTRA ATTRIBUTE VALUES --
2.41
~+; 1; y; @ltime; n; us1; ; all; all; 4
~+; 1; y; us1; 0; ; all; all; 4
~+; 1; y; @zfare; n; us2; ; all; all; 4
~+; 1; y; us2; 0; ; all; all; 4; q
~#
batchin=%t2%%t7%\%1%\busnode.extatt_%z%
2.41
~+; 3; @atype; n; all; 1,2; n; y
~+; 3; @zone; n; all; 1,3; n; y; q
batchin=%t2%%t7%\%1%\railnode.extatt_%z%
2.41
~+; 3; @pspac; n; all; 1,2; n; y
~+; 3; @pcost; n; all; 1,3; n; y
~+; 3; @zone; n; all; 1,4; n; y; q
batchin=defaults_base_year\per3_timau.txt
2.41
~+; 3; tmpl1; all; 1,2,3; n; y
~+; 1; y; @hwytm; n; tmpl1; ; all; all; 2; 2; q
~/
~# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~# BATCHIN 17-12-0001 PARK-N-RIDE PARKING FILE BEGINNING C12Q1 scenario 200
~#~+;~?x<200;~$>skip
~#batchin=%t2%%t7%\17120001_parking.csv
~#2.41
~#~+; 3; @pspac; n; all; 1,2; n; y
~#~+; 3; @pcost; n; all; 1,3; n; y; q
~#~:skip
~# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~/
~# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~# BATCHIN CERMAK ROAD BRT PARKING FILE FROM C03 (NO LONGER USED)
~#~$>skip
~#batchin=%t2%%t7%\cermakbrt_parking.csv
~#2.41
~#~+; 3; @pspac; n; all; 1,2; n; y
~#~+; 3; @pcost; n; all; 1,3; n; y; q
~#~:skip
~# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~/
~/
~# -- BRING IN CONGESTED HIGHWAY TIME --
~# -- THIS SECTION DELETED BECAUSE WE DO IT IN GLOBAL ITERATIONS CEB 11-1-2017
~#
~/ -- WRITE NETWORK STATISTICS REPORT --
~>>%t3%
~" =============================================================================================
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~"   -- NETWORK %t4%: DIRECTIONAL MILES BY SERVICE TYPE --
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~>>
~#
2.41
~+; 1; n; len; ; mod=BE; ; 2; 2
~+; 1; n; len; ; mod=LPQ; ; 2; 2
~+; 1; n; len; ; mod=C; ; 2; 2
~+; 1; n; len; ; mod=M; ; 2; 2; q
~/
~>>%t3%
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~"   -- NETWORK %t4%: SERVICE MILES BY SERVICE TYPE --
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~>>
~#
~# ## -- Time period duration (minutes) -- ##
~r1=180
~+;~?y=5;~r1=420
~#r1
2.41
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; %r1%/hdwy*len; ; mod=BE; ; mod=BE; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; %r1%/hdwy*len; ; mod=LPQ; ; mod=LPQ; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; %r1%/hdwy*len; ; mod=C; ; mod=C; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; len; ; mod=M; ; mod=M; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4; q
~/
~>>%t3%
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~"   -- NETWORK %t4%: SERVICE HOURS BY SERVICE TYPE --
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~>>
~#
2.41
~+; 1; y; us2; (%r1%/hdwy*@ltime)/60; ; mod=BE; ; mod=BE; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; (%r1%/hdwy*@ltime)/60; ; mod=LPQ; ; mod=LPQ; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; (%r1%/hdwy*@ltime)/60; ; mod=C; ; mod=C; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; @ltime/60; ; mod=M; ; mod=M; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4; q
~/
~>>%t3%
~" =============================================================================================
~>>
~#
~/ -- REPORT BUS ROUTE VMT --
2.41
~+; 1; y; us2; len; ; *; mod=BEPLQ; ; 2; 2
~+; 1; y; us2; 0; ; *; *; 4
~/ -- REPORT PARKING DATA --
~+; 1; n; @pcost; ; all; 2; 2
~+; 1; n; @pspac; ; all; 2; 2; q
~/
~/ -- REPORT NETWORK SUMMARY --
~+;2.14; 3; 2; q
~+;2.41; 1; n; lan*len; ; mod=BEPLQb; ; 2; 2; q
~/
~#
~# ## -- Import Shape Points -- ##
~t8=report\scen_%s%_transit_linkshape.rpt
~!if exist %t8% (del %t8%)
~!emmelinkshp -i %t2%linkshape\linkshape_%1%.in >> %t8% 
~#
~y+5
~z+1 
~+;~?y<9;~$loop
~/******************************************************************                   
~#
~$>end
~/
~:error
~/ ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~/        SUBMIT WITH 3-DIGIT TRANSIT SCENARIO !!!!!!
~/ ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~#
~:end
~o=6
batchin=
reports=
s=
~# ## -- Run check to identify transit network errors -- ##
~!prep_macros\build_transit_error_check.bat %1%
~/ end of macro

