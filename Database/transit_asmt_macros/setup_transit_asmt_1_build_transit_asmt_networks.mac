
~# setup_transit_asmt_1_build_transit_asmt_networks.mac
~#   Craig Heither, rev. 03-20-2023
~#
~#  ************************************************************************************************
~#    Macro used to create time-of-day transit networks for assignment
~#
~#    Must provide 3-digit scenario & path to network transaction files when submitting
~#    (e.g. "~<transit_asmt_macros\setup_transit_asmt_1_build_transit_asmt_networks.mac 100 M:\catslib\modelprod\c22q2\")
~#
~#  ************************************************************************************************
~# =================================================================================================
~z=%0%
~/ +;~?!z=3;~$>error
~/
~# ################################
~# -- SET PATH TO BATCHIN FILES --
~t2=%2%
~t7=transit
~# ################################
~/ ## - batchin source  = %t2%\%t7%\%1% - ##
~#
~#
~/ -- SET SCENARIO --
on=25                       /set to 4-digit date
~o|39
~#
~/ -- SET VARIABLES --
~x=%1%
~t1=2019
~+;~?x=200;~t1=2025
~+;~?x=300;~t1=2030
~+;~?x=400;~t1=2035
~+;~?x=500;~t1=2040
~+;~?x=600;~t1=2045
~+;~?x=700;~t1=2050
~t5=transit asmt network - %d%
~t6=%t1%
~#
~y=1  / transit assignment network counter [1,3,5,7]
~z=1  / transit transaction file counter [1-4]
~#
~:loop
~t4=%z%
~/ -- BUILD TRANSIT NETWORK %t4% --
~+;~?y=1;~t1=%t6% Night (6pm-6am)
~+;~?y=3;~t1=%t6% AM (6am-9am)
~+;~?y=5;~t1=%t6% Midday (9am-4pm)
~+;~?y=7;~t1=%t6% PM (4pm-6pm)
~#
~#
~# ## -- Assigment networks x21-x24 -- ##
~+;~x=%1%;~x+%y%;~x+20
s=%x%
~+;~?e;~$>new
~+;1.22; 2; %x%; y; q
~:new
~+;1.22; 1; %x%; %t1% %t5%; y; q
~#
~/ -- SET REPORT --
~t3=report\build_%s%transit.rpt
~!if exist %t3% (del %t3%)
reports=%t3%
~#
~/ -- Network --
batchin=%t2%\%t7%\tranmodes.txt
~+;2.01; 1; 2; q
batchin=%t2%\%t7%\transveh.txt
~+;2.02; 1; 2; q
batchin=%t2%\%t7%\%1%\bus.network_%t4%
~+;2.11; 2;
batchin=%t2%\%t7%\%1%\rail.network_%t4%
~+;2.11; 2;
batchin=%t2%\%t7%\%1%\access.network_%t4%
~+;2.11; 2;
~/ -- Transit Lines --
batchin=%t2%\%t7%\%1%\rail.itinerary_%t4%
~+;2.21; 2
batchin=%t2%\%t7%\%1%\bus.itinerary_%t4%
~+;2.21; 2
~/
~/ -- CREATE EXTRA ATTRIBUTES --
2.42
~+; 1; y
~+; 2; 1; @pcost; avg. daily parking cost at station; 0
~+; 2; 1; @atype; area type for on-street parking; 0
~+; 2; 1; @pspac; off-street parking spaces at station; 0
~+; 2; 1; @zone; CMAP zone; 0
~+; 2; 4; @ltime; line service time in minutes; 0
~+; 2; 4; @hwytm; AM peak congested hwy time from scenario %1%; 0
~+; 2; 4; @zfare_link; incremental zone fare; 0  /# differs from skim network, assignment will create/use @zfare
~+; 2; 1; @timbo; Base boarding time by station type, min; 0
~+; 2; 3; @easeb; Ease of boarding 1=worst, 4=best; 0
q
~/
~/ -- SET EXTRA ATTRIBUTE VALUES --
2.41
~+; 1; y; @ltime; n; us1; ; all; all; 4
~+; 1; y; us1; 0; ; all; all; 4
~+; 1; y; @zfare_link; n; us2; ; all; all; 4
~+; 1; y; us2; 0; ; all; all; 4; q
~#
batchin=%t2%\%t7%\%1%\busnode.extatt_%t4%
2.41
~+; 3; @atype; n; all; 1,2; n; y
~+; 3; @zone; n; all; 1,3; n; y; q
batchin=%t2%\%t7%\%1%\railnode.extatt_%t4%
2.41
~+; 3; @pspac; n; all; 1,2; n; y
~+; 3; @pcost; n; all; 1,3; n; y
~+; 3; @zone; n; all; 1,4; n; y; q
~# BATCHIN BUS BASE BOARDING TIME
batchin=transit_asmt_macros\data\bus_node_extra_attributes.csv
2.41
~+; 3; @timbo; n; all; 1,4; n; y; q
~# BATCHIN RAIL STATION BASE BOARDING TIME
batchin=transit_asmt_macros\data\rail_node_extra_attributes.csv
2.41
~+; 3; @timbo; n; all; 1,6; n; y; q
~# Ensure minimum values
2.41
~+|1|y|@timbo|n|@timbo.max.0.5| |5000,29999| |4
~+|1|y|@timbo|n|@timbo.max.1.5| |30000,49999| |4|q
~# INPUT EASE OF BOARDING CATEGORY
/  1=staircase (worst) is default
/  2=low floor/kneeling
/  3=level w/platform (best)
batchin=transit_asmt_macros\data\boarding_ease_by_line_id.csv
2.41
~+|3|@easeb|n|all|1,2|n|y|q
~# Ensure minimum values
2.41
~+|1|y|@easeb|n|@easeb.max.2| |mod=BELPQ| |4
~+|1|y|@easeb|n|@easeb.max.3| |mod=C| |4
~+|1|y|@easeb|n|@easeb.max.1| |mod=M| |4
~# -- Minor Adjustments --
~+|1|y|@easeb|n|1| |mod=P| |4
~+|1|y|@easeb|n|2| |lin=cbl___| |4
~+|1|y|@easeb|n|2| |lin=mbn___| |4
~+|1|y|@easeb|n|0| |lin=mme___| |4
~+|1|y|@easeb|n|2| |lin=mnw___| |4
~+|1|y|@easeb|n|2| |lin=mu____| |4|q
~# -- Increase wait convenience factor for MME & CBL --
2.41
~+|1|y|ut3|1| |lin=mme___| |4
~+|1|y|ut3|1| |lin=cbl___| |4
~+|1|y|ui3|ut3*10| |2|lin=mme___| |all|4
~+|1|y|ui3|ut3*5 | |2|lin=cbl___| |all|4|q
~/
~#
~/ SET TRANSIT TIME FUNCTION (ft1=normal, ft2-BRT)
/ Delete existing functions
4.12
~+; 4; ft1,ft2;
~?e
~+|q|~$>ADD FUNCTIONS
~+|y|q
/ Add ft1 and ft2 both reference us1
~:ADD FUNCTIONS
4.12
~+; 2; ft1; us1; ; ft2; us1; ; ; q
~#
~/ PERIOD LENGTH (HOURS) should NT=12?
~+;~?y=1;~r1=6
~+;~?y=2;~r1=3
~+;~?y=3;~r1=7
~+;~?y=4;~r1=2
~#
~##
~/ -- WRITE NETWORK STATISTICS REPORT --
~>>%t3%
~" =============================================================================================
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~"   -- NETWORK %t4%: DIRECTIONAL MILES BY SERVICE TYPE --
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~>>
~#
2.41
~+; 1; n; len; ; mod=BE; ; 2; 2
~+; 1; n; len; ; mod=LPQ; ; 2; 2
~+; 1; n; len; ; mod=C; ; 2; 2
~+; 1; n; len; ; mod=M; ; 2; 2; q
~/
~>>%t3%
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~"   -- NETWORK %t4%: SERVICE MILES BY SERVICE TYPE --
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~>>
~#
2.41
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; %r1%/hdwy*len; ; mod=BE; ; mod=BE; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; %r1%/hdwy*len; ; mod=LPQ; ; mod=LPQ; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; %r1%/hdwy*len; ; mod=C; ; mod=C; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; len; ; mod=M; ; mod=M; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4; q
~/
~>>%t3%
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~"   -- NETWORK %t4%: SERVICE HOURS BY SERVICE TYPE --
~" * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~>>
~#
2.41
~+; 1; y; us2; (%r1%/hdwy*@ltime)/60; ; mod=BE; ; mod=BE; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; (%r1%/hdwy*@ltime)/60; ; mod=LPQ; ; mod=LPQ; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; (%r1%/hdwy*@ltime)/60; ; mod=C; ; mod=C; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4
~+; 1; y; us2; @ltime/60; ; mod=M; ; mod=M; ;2; 2
~+; 1; y; us2; 0; ; *; *; 4; q
~/
~>>%t3%
~" =============================================================================================
~>>
~#
~/ -- REPORT BUS ROUTE VMT --
2.41
~+; 1; y; us2; len; ; *; mod=BEPLQ; ; 2; 2
~+; 1; y; us2; 0; ; *; *; 4
~/ -- REPORT PARKING DATA --
~+; 1; n; @pcost; ; all; 2; 2
~+; 1; n; @pspac; ; all; 2; 2; q
~/
~/ -- REPORT NETWORK SUMMARY --
~+;2.14; 3; 2; q
~+;2.41; 1; n; lan*len; ; mod=BEPLQb; ; 2; 2; q
~/
~#
~# ## -- Import Shape Points -- ##
~t8=report\scen_%s%_transit_linkshape.rpt
~!if exist %t8% (del %t8%)
~!emmelinkshp -i %t2%\linkshape\linkshape_%1%.in >> %t8% 
~#
~/ NETWORK CHECK
~/ --- Ignore error for not finding report for scenario+5 ---
~# !prep_macros\build_transit_error_check.bat %s%
~#
~#
~y+2
~z+1
~+;~?z<5;~$loop
~/******************************************************************                   
~#
~$>end
~/
~:error
~/ ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~/   SUBMIT WITH TRANSIT SCENARIO AND PROCESSING CODE!!!!!!
~/ ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~#
~:end
~o=6
batchin=
reports=
s=
~/ end of macro
q
