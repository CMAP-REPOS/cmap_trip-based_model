
~# SUMMARIZE_TRANSIT_BOARDINGS.MAC
~#   Craig Heither, 10-25-2022
~#
~#    Summarize transit boardings for congested transit assignment.
~#     submit with: ~<transit_asmt_macros\summarize_transit_boardings.mac <3-digit scenario of transit NT period >
~#       e.g.: " ~<transit_asmt_macros\summarize_transit_boardings.mac 121 "
~#
~#  ************************************************************************************************
~# =================================================================================================
~o|39
~x=%1%
~z=%1%
~z+8   /Maximum scenario (actual plus 1): %z%
~y=1   /Line counter
~t3=transit_asmt_macros\report\Boarding_summary.csv
~!if exist %t3% (del %t3%)
~>>%t3%
~"Line,Scenario,TotalBoard,PassengerMiles
~>
~# ####
~#
~:full_loop
~#
~:scenario_loop
s=%x%
~?y=1
~+;~t1=CTA_Rail_Blue_Line;~t2=cbl___
~?y=2
~+;~t1=CTA_Rail_Brown_Line;~t2=cbr___
~?y=3
~+;~t1=CTA_Rail_Green_Line;~t2=cg____
~?y=4
~+;~t1=CTA_Rail_Orange_Line;~t2=cor___
~?y=5
~+;~t1=CTA_Rail_Pink_Line;~t2=cpk___
~?y=6
~+;~t1=CTA_Rail_Purple_Line;~t2=cpr___
~?y=7
~+;~t1=CTA_Rail_Red_Line;~t2=crd___
~?y=8
~+;~t1=CTA_Rail_Yellow_Line;~t2=cye___
~?y=9
~+;~t1=Metra_BNSF;~t2=mbn___
~?y=10
~+;~t1=Metra_Heritage_Corridor;~t2=mhc___
~?y=11
~+;~t1=Metra_Electric;~t2=mme___
~?y=12
~+;~t1=Metra_Milwaukee_North;~t2=mmn___
~?y=13
~+;~t1=Metra_Milwaukee_West;~t2=mmw___
~?y=14
~+;~t1=Metra_North_Central;~t2=mnc___
~?y=15
~+;~t1=Metra_Rock_Island;~t2=mri___
~?y=16
~+;~t1=Metra_SouthWest_Service;~t2=msw___
~?y=17
~+;~t1=Metra_UP_North;~t2=mun___
~?y=18
~+;~t1=Metra_UP_Northwest;~t2=mnw___
~?y=19
~+;~t1=Metra_UP_West;~t2=muw___
~?y=20
~+;~t1=NICTD_South_Shore;~t2=mss___
~?y=21
~+;~t1=CTA_Bus_Regular;~t2=mod=B
~?y=22
~+;~t1=CTA_Bus_Express;~t2=mod=E
~?y=23
~+;~t1=Pace_Bus_Regular;~t2=mod=P
~?y=24
~+;~t1=Pace_Bus_Express;~t2=mod=Q
~?y=25
~+;~t1=Pace_Bus_Local;~t2=mod=L
~#
~# ##-- Boardings -- ##
2.41
~+;1;n;board; ;%t2%; ;*;5;4;ms900;allbrd;All line boards;1
~+;1;n;voltr*len; ;%t2%; ;*;5;4;ms901;pssmle;Passenger miles;1;q
~#
~>>%t3%
~"%t1%,%s%,%ms900.0%,%ms901.0%
~>
~x+2
~+;~?x<%z%;~$scenario_loop
~# ##-- next line --##
~+;~x-8;~y+1
~+;~?y<26;~$full_loop
~#
~# ##-- Delete matrices --##
3.12
~+;2;ms900;y
~+;2;ms901;y;q
~/ summary done!
q
