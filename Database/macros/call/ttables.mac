~/
reports=report\iter_%ms98%\ttables_%s%.rxt
~t3=report\iter_%ms98%\tod_veh_trips_and_VOT.txt
~#
~o|39
~/**********************************************************************
~/**********************************************************************
~/      ttables %1% %2% %3% %4%
~/   Where %1% = three digit alternative ID number
~/         %2% = TOD period
~/         %3% = 13 if HOV2 vehicles are class S
~/             = 92 if HOV2 vehicles are class H
~/         %4% = 13 if HOV3 vehicles are class S
~/             = 93 if HOV3 vehicles are class H
~/
~/   Macro to prepare period %2% trip tables
~/   This version prepares six vehicle class tables where class H
~/    (mf18) can be zero depending on %3% and %4% parameter values.
~/
~/           Revised by DBE Dec2002 for Mobile6 changes
~/           Revised by DBE 18Sep2003 for ORD adjustment
~/           Revised by DBE 19Nov2003 for put/get
~/           Revised by DBE 20Apr2004 for 5 classes
~/           Revised by DBE 1OCT2004 for fulliter batch
~/           Revised by DBE 17nov2004 to use replaceable parameter %1%
~/            in reports path.
~/
~#        Heither, 06/21/2010 - park-and-ride trips included in TOD auto
~#                              demand table. Matrix convolution performed
~#                              to extract first leg demand using indexed
~#                              station zone.
~#
~#        Heither, 03/10/2011 - corrected & renamed time period matrix labels.
~#
~#        Heither, 12/06/2012 - detailed TOD trip purpose demand written to report.
~#
~#        Heither, 06/14/2013 - calls new kzone file created by renumber.transit.skims.
~#
~#        Heither, 10/30/2014 - matrix values adjusted for non-work HOV procedures.
~#
~#        Ferguson, 02/10/2015 - change from 6 classes to 7 classes by dividing HOV into HOV2 and HOV3+
~# 
~#        Ferguson, 04/29/2015 - tod factor registers renumbered to accommodate expanded SOV and HOV factors.
~#	      Bozic 12/15/2015 - revised to reference existing peak hour index matrix instead of batching in
~#	                     and do not delete mf"kzonep" afterwards 11-2-2017 changed to kasmpt assignment index matrix
~#
~#        Heither, 07-15-2016 - implements VOT calculations for toll generalized cost.
~#
~#        Heither, 09-02-2016 - User Class toll perception factors included in VOT calculations for toll generalized cost.
~#        
~#        Bozic, 4-14-2017- add separate matrix mf848 milwaukee trips to mf8
~#        Ferguson, 9/18/2018 - updated r250 to number of zone17 zones
~/**********************************************************************
~/**********************************************************************
~r248=%3%                       /* store matrix value for HOV2 trips
~r249=%4%                       /* store matrix value for HOV3+ trips
~r250=3632                      /* max. non-POE zone
~#
~/batchin zone flags for fractioning triptables
~/ add by kww 6/05 to ease shifting between zone systems
~/
batchin=data/tod_flags.in
3.11
1
q
batchin=batchin
3.12
~+;4;3;mo"pflag";md"qflag";y;tod fraction flags;0
q
~/ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~/ Get TOD Trip Table Factors
~/ CMH 06/08 - to eliminate hardcoding of factors in macros (see documentation in Excel file)
~/
~y=%2%
~/ -- Get TOD Factors
~<<data\tod_factors.p%y%
~z=1
~:loop1
~+;~t0=~@;~?e;~$>eof1
~rz=%t0%
~/r%z%=%rz%
~z+1
~+;~?z<27;~$loop1
~:eof1
~/end of reading factors
~/
~/ -- Get TOD Occupancy Rates
~<<data\tod_occ.p%y%
~z=31
~:loop2
~+;~t0=~@;~?e;~$>eof2
~rz=%t0%
~/r%z%=%rz%
~z+1
~+;~?z<40;~$loop2
~:eof2
~/end of reading occ
~/
~/ -- Get Directional Splits
~<<data\directional.splits
~z=41
~:loop3
~+;~t0=~@;~?e;~$>eof3
~rz=%t0%
~/r%z%=%rz%
~z+1
~+;~?z<57;~$loop3
~:eof3
~/end of reading splits
~/
~/ Set Matrix labels
~t1=p1. 8pm-6am
~+;~?y=2;~t1=p2. 6am-7am
~+;~?y=3;~t1=p3. 7am-9am
~+;~?y=4;~t1=p4. 9am-10am
~+;~?y=5;~t1=p5. 10am-2pm
~+;~?y=6;~t1=p6. 2pm-4pm
~+;~?y=7;~t1=p7. 4pm-6pm
~+;~?y=8;~t1=p8. 6pm-8pm
~#
~/**********************************************************************
~/
~/   Initialize period trip tables mf13 - mf18 
~/
~/**********************************************************************
3.21
~+;1;y;mf13;y;modeS;auto veh (mod=S) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf14;y;bplt;b plate truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf15;y;ltrk;light truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf16;y;mtrk;medium truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf17;y;htrk;heavy truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf18;y;modeH;auto veh (mod=H) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf92;y;modeH2;auto veh (mod=H) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf93;y;modeH3;auto veh (mod=H) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;q
~#
~#**********************************************************************
~#  ## ---  Value-of-Time Data --- ##
~#**********************************************************************
~# ## -- Value of Time (dollars per hour) -- ##
~r100=12     / VOT for SOV1 HBW low income
~r101=20     / VOT for SOV1 HBW high income
~r102=18     / VOT for HOV2 HBW low income
~r103=30     / VOT for HOV2 HBW high income
~r104=27     / VOT for HOV3 HBW low income
~r105=45     / VOT for HOV3 HBW high income
~r106=12     / VOT for SOV1 HBO & NHB
~r107=18     / VOT for HOV2 HBO & NHB
~r108=27     / VOT for HOV3 HBO & NHB
~r109=20     / VOT for B-Plate truck
~r110=25     / VOT for Light truck
~r111=25     / VOT for Medium truck
~r112=40     / VOT for Heavy truck
~r113=18     / VOT for External auto
~r114=30     / VOT for AirPass
~# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~# ## -- Vehicle Class Perception Factors -- ##
~#      Used to reflect that User Classes may not perceive 100% of the
~#      cost of a toll (effectively increases their VOT in toll generalized cost). 
~#      -  Commercial vehicle drivers may be reimbursed for tolls or their
~#         Just-in-Time delivery responsibilities may play a much larger role in
~#         route decisions than tolls. 
~#      -  Auto I-PASS users may not perceive the entire cost because they are not
~#         paying out-of-pocket for each transaction (i.e., only see monthly credit
~#         card statement).
~r100*9.00     / SOV1 HBW low income
~r101*9.00     / SOV1 HBW high income
~r102*9.00     / HOV2 HBW low income
~r103*9.00     / HOV2 HBW high income
~r104*9.00     / HOV3 HBW low income
~r105*9.00     / HOV3 HBW high income
~r106*8.00     / SOV1 HBO & NHB
~r107*8.00     / HOV2 HBO & NHB
~r108*8.00     / HOV3 HBO & NHB
~r109*2.00     / B-Plate truck
~r110*2.00     / Light truck
~r111*2.00     / Medium truck
~r112*2.00     / Heavy truck
~r113*3.00     / External auto
~r114*3.00     / AirPass
~#**********************************************************************
~#
~#
~+;~?y>1;~$>not_TOD1
~+;3.21;1;y;mf59;y;parkrd;drive to transit station;~?q=1;y;0;0; ; ;n;1;q
~/
~/ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~#
~# ## PERFORM CONVOLUTION TO GET TRIPS FROM ORIGIN ZONE TO STATION ZONE ##
~# ## ONLY NECESSARY ONCE - DURING TOD 1 ##
~+;~?y>1;~$>not_TOD1
~#
~#
~# -- create combined HW transit demand matrix (high + low income) --
3.21
~+;1;y;mf"hwtran";y;HW High and Low inc transit trips;0;mf40+mf41; ; ;n;2
~#
~# -- create matrix of origins for convolution --
~+;1;y;mo"ptemp";y;origins to extract first leg demand;0;p; ; ;y;1,%r250%; ;2;q
~#
~# -- perform convolution --
3.23
1
mf837                    ~/first operand matrix
==                       ~/matrix combination operator
mo"ptemp"                ~/second operand matrix
y                        ~/use transpose of second operand matrix?
*                        ~/masking operator 1
n                        ~/same masking operator for all intermed. zones?
mf"hwtran"               ~/matrix containing masking value
2                        ~/origin to intermed. zone index
 
 
mf"temppr"               ~/results matrix
y                        ~/initialize matrix?
temp drive to transit
0
y                        ~/submatrix?
1,%r250%                 ~/origin zones
 
1,%r250%                 ~/intermed. zones
 
1,%r250%                 ~/destinations


y
2
q
~#
~# simplified conversion of matrix from P-A format to O-D format
3.21
~+;1;y;mf59;y;parkrd;drive to transit station;~?q=1;y;0;(mf"temppr"+mf"temppr"')/2; ; ;n;2;q
~# 
~# -- delete temporary matrices --
3.12
~+;2;mf"hwtran";y
~+;2;mf"temppr";y
~+;2;mo"ptemp";y;q
~# 
~:not_TOD1
~/ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~/**********************************************************************
~/
~/    Calculate mf13 as time period mode S auto vehicles and if mode H
~/      assignment desired mf18 mode H auto vehicles:
~/      convert/combine hbw person trips by occupancy level from mode
~/      choice to auto vehicle trips
~/    modified to read zone flag array rather than hard coded zones, kww 6/05
~/
~/**********************************************************************
3.21
1
yes
mf13
no
~/  Home to work:SOV1
(md"qflag".eq.0)*
   ((%r45%*mf101)*%r5%)+
(md"qflag".eq.1)*
   ((%r41%*mf101)*%r1%)+
(md"qflag".ge.2)*
   ((%r49%*mf101)*%r9%)+
~/  Work to home:SOV1
(mo"pflag".eq.0)*
   ((%r47%*mf81)*%r7%)+
(mo"pflag".eq.1)*
   ((%r43%*mf81)*%r3%)+
(mo"pflag".ge.2)*
   ((%r51%*mf81)*%r11%)


no
2
~/
~# ## 
~# ## -- Calculate Component Trips by income-occupancy for VOT -- ##
~+;1;y;mf60;y;temp60;HW SOV low income tod %y%;~?q=1;y;0
~/  Home to work:SOV1 low income
(md"qflag".eq.0)*
   ((%r45%*mf50)*%r5%)+
(md"qflag".eq.1)*
   ((%r41%*mf50)*%r1%)+
(md"qflag".ge.2)*
   ((%r49%*mf50)*%r9%)+
~/  Work to home:SOV1 low income
(mo"pflag".eq.0)*
   ((%r47%*mf209)*%r7%)+
(mo"pflag".eq.1)*
   ((%r43%*mf209)*%r3%)+
(mo"pflag".ge.2)*
   ((%r51%*mf209)*%r11%)
~+; ; ;n;2
~#
~+;1;y;mf61;y;temp61;HW SOV high income tod %y%;~?q=1;y;0
~/  Home to work:SOV1 high income
(md"qflag".eq.0)*
   ((%r45%*mf53)*%r5%)+
(md"qflag".eq.1)*
   ((%r41%*mf53)*%r1%)+
(md"qflag".ge.2)*
   ((%r49%*mf53)*%r9%)+
~/  Work to home:SOV1 high income
(mo"pflag".eq.0)*
   ((%r47%*mf212)*%r7%)+
(mo"pflag".eq.1)*
   ((%r43%*mf212)*%r3%)+
(mo"pflag".ge.2)*
   ((%r51%*mf212)*%r11%)
~+; ; ;n;2
~#
~+;1;y;ms10;y;tmp10;store HW SOV;~?q=1;y;0;mf13; ; ;n;+;+;1;q
~t2=HW SOV trips
~>>%t3%
~"Period %y% %t2% : %ms10.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r248%
no
~/  Home to work:HOV2
mf%r248%+
(md"qflag".eq.0)*
   (put(%r46%*mf102)*%r6%/2.0)+
(md"qflag".eq.1)*
   ((%r42%*mf102)*%r2%/2.0)+
(md"qflag".ge.2)*
   (get(1)*%r10%/2.0)+
~/  Work to home:HOV2
(mo"pflag".eq.0)*
   (put(%r48%*mf82)*%r8%/2.0)+
(mo"pflag".eq.1)*
   ((%r44%*mf82)*%r4%/2.0)+
(mo"pflag".ge.2)*
   (get(2)*%r12%/2.0)


no
2
~/
~# ## 
~# ## -- Calculate Component Trips by income-occupancy for VOT -- ##
~+;1;y;mf62;y;temp62;HW HOV2 low income tod %y%;~?q=1;y;0
~/  Home to work:HOV2 low income
(md"qflag".eq.0)*
   (put(%r46%*mf51)*%r6%/2.0)+
(md"qflag".eq.1)*
   ((%r42%*mf51)*%r2%/2.0)+
(md"qflag".ge.2)*
   (get(1)*%r10%/2.0)+
~/  Work to home:HOV2 low income
(mo"pflag".eq.0)*
   (put(%r48%*mf210)*%r8%/2.0)+
(mo"pflag".eq.1)*
   ((%r44%*mf210)*%r4%/2.0)+
(mo"pflag".ge.2)*
   (get(2)*%r12%/2.0)
~+; ; ;n;2
~#
~+;1;y;mf63;y;temp63;HW HOV2 high income tod %y%;~?q=1;y;0
~/  Home to work:HOV2 high income
(md"qflag".eq.0)*
   (put(%r46%*mf54)*%r6%/2.0)+
(md"qflag".eq.1)*
   ((%r42%*mf54)*%r2%/2.0)+
(md"qflag".ge.2)*
   (get(1)*%r10%/2.0)+
~/  Work to home:HOV2 high income
(mo"pflag".eq.0)*
   (put(%r48%*mf213)*%r8%/2.0)+
(mo"pflag".eq.1)*
   ((%r44%*mf213)*%r4%/2.0)+
(mo"pflag".ge.2)*
   (get(2)*%r12%/2.0)
~+; ; ;n;2
~#
~+;1;y;ms11;y;tmp11;store HW HOV2;~?q=1;y;0;mf%r248%; ; ;n;+;+;1;q
~t2=HW HOV2 trips
~>>%t3%
~"Period %y% %t2% : %ms11.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r249%
no
~/  Home to work:HOV3+
mf%r249%+
(md"qflag".eq.0)*
   (put(%r46%*mf103)*%r6%/%r33%)+
(md"qflag".eq.1)*
   ((%r42%*mf103)*%r2%/%r31%)+
(md"qflag".ge.2)*
   (get(1)*%r10%/%r35%)+
~/  Work to home:HOV3+
(mo"pflag".eq.0)*
   (put(%r48%*mf83)*%r8%/%r34%)+
(mo"pflag".eq.1)*
   ((%r44%*mf83)*%r4%/%r32%)+
(mo"pflag".ge.2)*
   (get(2)*%r12%/%r36%)


no
2
~/
~# ## 
~# ## -- Calculate Component Trips by income-occupancy for VOT -- ##
~+;1;y;mf64;y;temp64;HW HOV3 low income tod %y%;~?q=1;y;0
~/  Home to work:HOV3+ low income
(md"qflag".eq.0)*
   (put(%r46%*mf52)*%r6%/%r33%)+
(md"qflag".eq.1)*
   ((%r42%*mf52)*%r2%/%r31%)+
(md"qflag".ge.2)*
   (get(1)*%r10%/%r35%)+
~/  Work to home:HOV3+ low income
(mo"pflag".eq.0)*
   (put(%r48%*mf211)*%r8%/%r34%)+
(mo"pflag".eq.1)*
   ((%r44%*mf211)*%r4%/%r32%)+
(mo"pflag".ge.2)*
   (get(2)*%r12%/%r36%)
~+; ; ;n;2
~#
~+;1;y;mf65;y;temp65;HW HOV3 high income tod %y%;~?q=1;y;0
~/  Home to work:HOV3+ high income
(md"qflag".eq.0)*
   (put(%r46%*mf55)*%r6%/%r33%)+
(md"qflag".eq.1)*
   ((%r42%*mf55)*%r2%/%r31%)+
(md"qflag".ge.2)*
   (get(1)*%r10%/%r35%)+
~/  Work to home:HOV3+ high income
(mo"pflag".eq.0)*
   (put(%r48%*mf214)*%r8%/%r34%)+
(mo"pflag".eq.1)*
   ((%r44%*mf214)*%r4%/%r32%)+
(mo"pflag".ge.2)*
   (get(2)*%r12%/%r36%)
~+; ; ;n;2
~#
~+;1;y;ms12;y;tmp12;store HW HOV3;~?q=1;y;0;mf%r249%; ; ;n;+;+;1;q
~t2=HW HOV3 trips
~>>%t3%
~"Period %y% %t2% : %ms12.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add in home-other trips
~/
~/**********************************************************************
~#
1
yes
mf13
no
mf13+
~/  Home to other:SOV1
((%r55%*mf104)*%r15%)+
~/  Other to home:SOV1
((%r53%*mf84)*%r13%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms23;y;tmp23;store HO SOV;~?q=1;y;0;((%r55%*mf104)*%r15%)+((%r53%*mf84)*%r13%); ; ;n;+;+;1;q
~t2=HO SOV trips
~>>%t3%
~"Period %y% %t2% : %ms23.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r248%
no
mf%r248%+
~/  Home to other:HOV2
((%r56%*mf105)*%r16%/2.0)+
~/  Other to home:HOV2
((%r54%*mf85)*%r14%/2.0)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms22;y;tmp22;store HO HOV2;~?q=1;y;0;((%r56%*mf105)*%r16%/2.0)+((%r54%*mf85)*%r14%/2.0); ; ;n;+;+;1;q
~t2=HO HOV2 trips
~>>%t3%
~"Period %y% %t2% : %ms22.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r249%
no
mf%r249%+
~/  Home to other:HOV3+
((%r56%*mf106)*%r16%/%r38%)+
~/  Other to home:HOV3+
((%r54%*mf86)*%r14%/%r37%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms21;y;tmp21;store HO HOV3;~?q=1;y;0;((%r56%*mf106)*%r16%/%r38%)+((%r54%*mf86)*%r14%/%r37%); ; ;n;+;+;1;q
~t2=HO HOV3 trips
~>>%t3%
~"Period %y% %t2% : %ms21.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add in non-home trips (and externals to mf13)
~/
~/**********************************************************************
~#
1
yes
mf13
no
mf13+
~/  non-Home:SOV1
(mf107*%r17%)+
~/  External
((mf08+mf848)*%r19%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms19;y;tmp19;store External;~?q=1;y;0;((mf08+mf848)*%r19%); ; ;n;+;+;1;q
~t2=External SOV trips
~>>%t3%
~"Period %y% %t2% : %ms19.2%
~>>
~+;3.21;1;y;ms20;y;tmp20;store NH SOV;~?q=1;y;0;(mf107*%r17%); ; ;n;+;+;1;q
~t2=NH SOV trips
~>>%t3%
~"Period %y% %t2% : %ms20.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r248%
no
mf%r248%+
~/  non-Home:HOV2
(mf108*%r18%/2.0)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms17;y;tmp17;store NH HOV2;~?q=1;y;0;(mf108*%r18%/2.0); ; ;n;+;+;1;q
~t2=NH HOV2 trips
~>>%t3%
~"Period %y% %t2% : %ms17.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r249%
no
mf%r249%+
~/  non-Home:HOV3+
(mf109*%r18%/%r39%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms18;y;tmp18;store NH HOV3;~?q=1;y;0;(mf109*%r18%/%r39%); ; ;n;+;+;1;q
~t2=NH HOV3 trips
~>>%t3%
~"Period %y% %t2% : %ms18.2%
~>>
3.21
~+;1;y;mf18;n;mf92+mf93; ; ;n;1;q
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add 
~/               air passenger rows and columns
~/    modified to read zone flag array rather than hard coded zones, kww 6/05
~/
~/**********************************************************************
1
yes
mf13
no
mf13+
mf10*("qflag".ge.2)*%r20%+
mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%


no
2
~/
~# ## 
~# ## 
~+;1;y;ms16;y;tmp16;store POE airport trips;~?q=1;y;0;(mf10*("qflag".ge.2)*%r20%)+(mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%); ; ;n;+;+;1;q
~t2=POE Airport trips
~>>%t3%
~"Period %y% %t2% : %ms16.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add 
~/      drive to transit station trips, Heither 06/2010
~/
~/**********************************************************************
1
yes
mf13
no
mf13+
mf59*(%r1%+%r3%)/2


no
2
~/
~# ## 
~# ## 
~+;1;y;ms15;y;tmp15;store P-n-R trips;~?q=1;y;0;mf59*(%r1%+%r3%)/2; ; ;n;+;+;1;q
~t2=Park-and-Ride trips
~>>%t3%
~"Period %y% %t2% : %ms15.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/  Prepare %t1% period truck trip tables
~/
~/**********************************************************************
~/
~/**********************************************************************
~/
~/    Calculate mf14 (as total b plate truck trips)
~/
~/**********************************************************************
1
yes
mf14
no
%r22%*mf04


no
2
~/
~# ## 
~# ## 
~+;1;y;ms14;y;tmp14;store b-plate trips;~?q=1;y;0;%r22%*mf04; ; ;n;+;+;1;q
~t2=B-Plate Truck trips
~>>%t3%
~"Period %y% %t2% : %ms14.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Calculate mf15, light truck trips
~/
~/**********************************************************************
1
yes
mf15
no
%r23%*mf05


no
2
~/
~# ## 
~# ## 
~+;1;y;ms13;y;tmp13;store light trk trips;~?q=1;y;0;%r23%*mf05; ; ;n;+;+;1;q
~t2=Light Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms13.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Calculate mf16, medium truck trips converted to veqs
~/
~/**********************************************************************
1
yes
mf16
no
%r24%*(2*mf06)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms12;y;tmp12;store med trk trips;~?q=1;y;0;%r24%*(2*mf06); ; ;n;+;+;1;q
~t2=Medium Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms12.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Calculate mf17, heavy truck trips converted to veqs plus
~/      externals, which are assumed to be primarily heavy trucks
~/
~/**********************************************************************
1
yes
mf17
no
%r25%*(3*mf07)+
%r26%*(3*mf09)


no
2
~# ## 
~# ##
~+;1;y;ms10;y;tmp10;store heavy trk trips;~?q=1;y;0;%r25%*(3*mf07); ; ;n;+;+;1;
~+;1;y;ms11;y;tmp11;store POE Heavy trk trips;~?q=1;y;0;%r26%*(3*mf09); ; ;n;+;+;1;q
~t2=Heavy Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms10.2%
~>>
~t2=POE Heavy Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms11.2%
~"- - - - - - - - - - - -
~>>
~# ## 
~/**********************************************************************
~# ## -- Calculations for VOT and Weight Factor -- ##
3.21
~# -- Store HBW Trip Values by income-occupancy --
~+;1;y;ms24;~?q=1;y;temp24;HW SOV low income Trips TOD %2%;~?q=1;y;0;mf60; ; ;n;+;+;2
~+;1;y;ms25;~?q=1;y;temp25;HW SOV high income Trips TOD %2%;~?q=1;y;0;mf61; ; ;n;+;+;2
~+;1;y;ms26;~?q=1;y;temp26;HW HOV2 low income Trips TOD %2%;~?q=1;y;0;mf62; ; ;n;+;+;2
~+;1;y;ms27;~?q=1;y;temp27;HW HOV2 high income Trips TOD %2%;~?q=1;y;0;mf63; ; ;n;+;+;2
~+;1;y;ms28;~?q=1;y;temp28;HW HOV3 low income Trips TOD %2%;~?q=1;y;0;mf64; ; ;n;+;+;2
~+;1;y;ms29;~?q=1;y;temp29;HW HOV3 high income Trips TOD %2%;~?q=1;y;0;mf65; ; ;n;+;+;2
~# -- Toll Multipliers (Weight Factors) for Individual Components --
~+;1;y;ms30;y;tmp30;HW SOV low income toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r100%; ; ;2
~+;1;y;ms31;y;tmp31;HW SOV high income toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r101%; ; ;2
~+;1;y;ms32;y;tmp32;HW HOV2 low income toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r102%; ; ;2
~+;1;y;ms33;y;tmp33;HW HOV2 high income toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r103%; ; ;2
~+;1;y;ms34;y;tmp34;HW HOV3 low income toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r104%; ; ;2
~+;1;y;ms35;y;tmp35;HW HOV3 high income toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r105%; ; ;2
~+;1;y;ms36;y;tmp36;HO-NH SOV toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r106%; ; ;2
~+;1;y;ms37;y;tmp37;HO-NH HOV2 toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r107%; ; ;2
~+;1;y;ms38;y;tmp38;HO-NH HOV3 toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r108%; ; ;2
~+;1;y;ms39;y;tmp39;External Auto toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r113%; ; ;2
~+;1;y;ms40;y;tmp40;AirPass Auto toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r114%; ; ;2
~# -- Calculated Weighted VOT by Assignment User Class --
~+;1;y;ms41;y;tmp41;User Class 1 VOT*Trips - TOD %y%;~?q=1;y;0;(ms24*%r100%)+(ms25+ms15)*%r101%+(ms23+ms20)*%r106%+(ms19*%r113%)+(ms16*%r114%); ; ;2
~+;1;y;ms42;y;tmp42;User Class 1 Trips - TOD %y%;~?q=1;y;0;ms24+ms25+ms15+ms23+ms20+ms19+ms16; ; ;2
~+;1;y;ms43;y;tmp43;User Class 1 Weighted VOT - TOD %y%;~?q=1;y;0;ms41/ms42; ; ;2
~+;1;y;ms44;y;tmp44;User Class 2 VOT*Trips - TOD %y%;~?q=1;y;0;(ms26*%r102%)+(ms27*%r103%)+(ms22+ms17)*%r107%+(ms28*%r104%)+(ms29*%r105%);+(ms21+ms18)*%r108%; ; ;2
~+;1;y;ms45;y;tmp45;User Class 2 Trips - TOD %y%;~?q=1;y;0;ms26+ms27+ms22+ms17+ms28+ms29+ms21+ms18; ; ;2
~+;1;y;ms46;y;tmp46;User Class 2 Weighted VOT - TOD %y%;~?q=1;y;0;ms44/ms45; ; ;2
~# -- Final Toll Multipliers (Weight Factors) --
~+;1;y;ms84;y;tmp84;User Class 1 toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/ms43; ; ;2
~+;1;y;ms85;y;tmp85;User Class 2 toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/ms46; ; ;2
~+;1;y;ms86;y;tmp86;b-plate truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r109%; ; ;2
~+;1;y;ms87;y;tmp87;light truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r110%; ; ;2
~+;1;y;ms88;y;tmp88;medium truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r111%; ; ;2
~+;1;y;ms89;y;tmp89;heavy truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r112%; ; ;2;q
~#
~#
~>>%t3%
~"Period %y% VOT Report:
~"
~"                                           Perceived VOT
~"                                         Relative to Tolls     Toll Multiplier
~"                            Trips             ($/hour)           (minutes/$)
~"Home-Work SOV low inc:   %ms24.2_>10%           %r100.2_>6%              %ms30.2_>5%
~"Home-Work SOV high inc:  %ms25.2_>10%           %r101.2_>6%              %ms31.2_>5%
~"Home-Other SOV trips:    %ms23.2_>10%           %r106.2_>6%              %ms36.2_>5%
~"Non-Home SOV trips:      %ms20.2_>10%           %r106.2_>6%              %ms36.2_>5%
~"External Auto trips:     %ms19.2_>10%           %r113.2_>6%              %ms39.2_>5%
~"Air Passenger trips:     %ms16.2_>10%           %r114.2_>6%              %ms40.2_>5%
~"Park & Ride trips:       %ms15.2_>10%           %r101.2_>6%              %ms31.2_>5%
~"Overall SOV trips:       %ms42.2_>10%           %ms43.2_>6%              %ms84.2_>5%

~"Home-Work HOV2 low inc:  %ms26.2_>10%           %r102.2_>6%              %ms32.2_>5%
~"Home-Work HOV2 high inc: %ms27.2_>10%           %r103.2_>6%              %ms33.2_>5%
~"Home-Other HOV2 trips:   %ms22.2_>10%           %r107.2_>6%              %ms37.2_>5%
~"Non-Home HOV2 trips:     %ms17.2_>10%           %r107.2_>6%              %ms37.2_>5%
~"Home-Work HOV3 low inc:  %ms28.2_>10%           %r104.2_>6%              %ms34.2_>5%
~"Home-Work HOV3 high inc: %ms29.2_>10%           %r105.2_>6%              %ms35.2_>5%
~"Home-Other HOV3 trips:   %ms21.2_>10%           %r108.2_>6%              %ms38.2_>5%
~"Non-Home HOV3 trips:     %ms18.2_>10%           %r108.2_>6%              %ms38.2_>5%
~"Overall HOV trips:       %ms45.2_>10%           %ms46.2_>6%              %ms85.2_>5%

~"B-Plate Truck trips:     %ms14.2_>10%           %r109.2_>6%              %ms86.2_>5%
~"Light Duty Truck trips:  %ms13.2_>10%           %r110.2_>6%              %ms87.2_>5%
~"Medium Duty Truck trips: %ms12.2_>10%           %r111.2_>6%              %ms88.2_>5%
~"Heavy Duty Truck trips:  %ms10.2_>10%           %r112.2_>6%              %ms89.2_>5%
~"External Truck trips:    %ms11.2_>10%           %r112.2_>6%              %ms89.2_>5%
~"------------------------------------------------------------------------------------------------------
~>>
~#
~/
~# ## DELETE REMAINING TEMPORARY MATRICES DURING PERIOD 8 ##
~+;~?y<8;~$>not_TOD8
3.12
~+;2;mf59;y;q
~:not_TOD8
~# 
~# ## -- Delete temp matrices ms10-ms46 --
~z=10
~:delms
~+;3.12;2;ms%z%;y;q
~z+1
~+;~?z<47;~$delms
~#
~# ## -- Delete temp matrices mf60-mf65 --
~z=60
~:delmf
~+;3.12;2;mf%z%;y;q
~z+1
~+;~?z<66;~$delmf
~#
batchin=
reports=
~/
~/**********************************************************************
