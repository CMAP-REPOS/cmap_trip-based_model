~# finish.transit.skims <3 digit id> <global iteration number>
~#  ****************************************************************************************
~#
~#   Macro moves peak ransit skim matrices to final locations in preparation of regional model run.
~#   Also punches intermediate station zone matrices.
~#
~#
~#     Revisions:
~#
~#		Heither, 07-06-2010 - coding added to punch matrix of zones with no
~#                   transit access/egress (intrazonal only) for use by iter.master5c.mac.
~#
~#     Bozic, 10-17-2012 - coding added to create intermediate station zone matrix transit assignment.
~#
~#    Heither, 05-22-2013 - revisions to intermediate station zone matrix for transit assignment
~#                       based on validation results.
~#			- if intrazonal trip or (if transit service available and trip originates 
~#			  in Chicago) then leave origin, otherwise use indexed station zone.
~#     Bozic, 12-14-2015 revised to no longer export kzone, kasmt and notransit matrices
~#			they are stored along with their backups in mf832-mf834 and will be
~#			referenced there when needed for highway and transit assignments.
~#
~#    Heither, 02-19-2016 - batch mode indicator added so can be called from transit_skim.bat.
~#
~#    Bozic, 11-2-2017 - revised to work with all matrices in the 800 and 900 ranges and will not write 
~#                      over model results.  This is for transit skimming during each global iteration.
~#                      also always works in batch mode. Does peak and offpeak.
~#                  
~#  ****************************************************************************************
~# -------------------------------------------------------------------------------------------------------
~#
~# 
~#
~y=0
~o|39
~/  max. non-POE zone
~r1=1944  
~#3 digit scenario number
~z=%1%
~:pk_op_loop
~y+1
~/set register for the matrix series 800=peak (8) 900=offpeak (9)
~?y=1
~r2=8
~?y=2
~r2=9
~+;s=%z%;~?e;~$>bad_scen
~/ Transit scenario argument is valid.
~#
~#
~# Create kzone matrix (constrained by indexed transit/auto only value of 0.3 or less) & adjust header
3.21
~+;1;y;mf%r2%35;y;kzonep;Intermed stn zn %z%;~?q=1;y;0;mf%r2%21; ;mf%r2%34;0,0.3,include;n;2;q
~# ____________________________________________________________________________________________
~# Create matrix to flag zones that should not have intrazonal transit trips & adjust header
~# ____________________________________________________________________________________________
2.41
~+;1;y;ui3;0; ;all;4
~#  ## flag transit access/egress links
~+;1;y;tmpl1;1; ;mod=uvwxyz; ;4
~#  ## sum link flags for each node (zone)
~+;1;y;ui3;tmpl1+tmpl1r; ;4;all;4
~#  ## switch value to flag nodes with no transit access/egress
~+;1;y;ui3;(ui3.eq.0); ;all;4;q
3.21
~#  ## create matrix flagging zones with no transit access/egress (intrazonal only)
~+;1;y;mf%r2%36;y;intrap;no intrazonal transit flag %z%;~?q=1;y;0;ui3; ; ;y;1,%r1%; ;1,%r1%; ;2
~+;1;y;mf%r2%36;n;(p.eq.q)*mf%r2%36; ; ;n;2;q
2.41
~+;1;y;ui3;0; ;all;4;q
~#
~# ____________________________________________________________________________________________
~# Create indexed station zone matrix for transit assignment: 
~#   station zone equals origin if: transit service is present in walk access skim & service
~#   originates in Chicago or trip is intrazonal
~# ____________________________________________________________________________________________
3.21
~#  ## Keep the original zone if: the trip is intrazonal or if there is skimmed transit service available (mf8) and service
~#  ##   originates within the City of Chicago.
~#  ## Otherwise, apply the indexed station zone.
~#  ##   (mf8 is unindexed transit in-vehicle time)
~#  ##
~+;1;y;mf%r2%37;y;kasmtp;indexed stn zn for tran asmt %z%;~?q=1;y;0 
((mf%r2%08.lt.1000 .and. ga(p).eq.1).or.(p.eq.q))*p +
((mf%r2%08.ge.1000 .or. ga(p).ne.1).and.(p.ne.q))*mf%r2%21
~+; ; ;y;1,%r1%; ;1,%r1%; ;2;q
~#
2.41
~+;1;y;ui3;0; ;all;4;q
~#
~/RECALCULATE HEADWAY MATRICES
3.21
~+;1;y;mf%r2%38;y;phdway;%z% indxd wait time x 2 I%2%;~?q=1;y;0;mf%r2%24*2; ; ;n;1; 
~+;1;y;mf%r2%39;y;ohdway;%z% indxd hdwy x 1.25 I%2%;~?q=1;y;0;mf%r2%24*1.25; ; ;n;1; 
q
~# ____________________________________________________________________________________________
~/DELETE UNNEEDED MATRICES
~# ____________________________________________________________________________________________
3.12
~+;2;mo%r2%01;y
~+;2;mo%r2%02;y
~+;2;mo%r2%03;y
~+;2;mo%r2%04;y
~+;2;mo%r2%05;y
~+;2;mf%r2%03;y
~+;2;mf%r2%04;y
~+;2;mf%r2%05;y
~+;2;mf%r2%06;y
~+;2;mf%r2%07;y
~+;2;mf%r2%08;y
~+;2;mf%r2%09;y
~+;2;mf%r2%10;y
~+;2;mf%r2%11;y
~+;2;mf%r2%12;y
~+;2;mf%r2%13;y
~+;2;mf%r2%15;y
~+;2;mf%r2%16;y
~+;2;mf%r2%17;y
~+;2;mf%r2%18;y
~+;2;mf%r2%19;y
q
~/increment scenario number 5 for offpeak
~z+5
~?y<2
~$pk_op_loop
~o=6
~$>end
~/
~#
~:bad_scen
~/
~/ ++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~/   3-DIGIT SCENARIO DOES NOT MATCH EMMEBANK DATA!!!!!!!
~/ ++++++++++++++++++++++++++++++++++++++++++++++++++++++++
~$>skip
~#
~:end
3.14
2
m
1
~:skip
~/ -- end of macro --
~+;q;q
