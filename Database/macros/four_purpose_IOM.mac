~/
~/*****************************************************************************
~/
~/    four_purpose_IOM.mac %1%
~/        where %1% = three digit alternative ID
~/
~/*****************************************************************************
~/
~/    Macro to run each of the four purpose IOM macros in one pass.  
~/    Passes individual replaceable parameters and lists matricies at end.
~/
~/*****************************************************************************
~/
~/    Revised by RWE and DBE October 2009 for I-290 HOV
~/    Revised 02/2010 by CMH - added max. zone to distribute argument to IOM
~/                             macros
~/    Revised Heither, 05-31-2016: Procedures to distribute work trips to
~/                                 greater Milwaukee
~/    Ferguson 8/21/2018: Updated 'number of zones to distribute' argument in
~/                        trip purpose macros to zone17
~/    Ferguson 9/10/2018: Updated Milwaukee work trip POEs to zone17
~/    NRF 6/13/2019: Do Milwaukee cleanup in global iteration 2 (last distr iter)
~/
~/*****************************************************************************
~/
~o|39
~#
~x=%ms98%
~+;~?x=0;~$>Milw_setup
~$>skip
~#
~:Milw_setup
~# ## -- SET UP FOR MILWAUKEE WORK TRIP DISTRIBUTION
~# ## -- ADD X-COORD,Y-COORD,SQ MILES FOR ZONE 3633
~# ## -- USE ZONE 3633 FOR DISTRIBUTION, THEN MOVE TRIPS TO ZONE 3634
3.21
~+;1;y;mo11;n;1; ; ;y;3633; ;1;
~+;1;y;mo12;n;xi; ; ;y;3633; ;1;
~+;1;y;ms100;~?q=1;y;zn3633;zone 3633 y-coord;~?q=1;y;0;yi; ; ;y;3633; ;+;1;
~+;1;y;md12;n;ms100; ; ;y;3633; ;1;
~#
~# ## -- ADD WORK ATTRACTIONS TO ZONE 3633
~# ## -- SET WORK ATTRACTIONS FOR HIGH/LOW INCOME TO MS96 (ASSUME EQUAL NUMBER OF TRIPS HOME-WORK AND WORK-HOME)
~# ## -- WILL CONVERT FROM P-A TO O-D FORRMAT LATER
~+;1;y;md1;n;ms96; ; ;y;3633; ;1;
~+;1;y;md2;n;ms96; ; ;y;3633; ;1;q
~#
~# ## -- SAVE A PRISTINE COPY OF MF8 TO BE RE-USED
3.21
~+;1;y;mf800;y;mf8copy;copy of original mf8;~?q=1;y;0;mf8; ; ;n;1;q
~#
~#
~:skip
~/**********************************************************************
~/
~/   Run each of the trip purpose type IOM macros in turn.
~/
~/*********************************************************************
~/
~/  low income home-work distribution
~# ## -- Add 14th argument (3633) for Milwaukee work trip distribution --
~<macros/call/amhwIOM_L.mac 01 01 32 33 36 67 05 06 68 69 %ms98% %1% 3632 3633
~/
~/  high income home-work distribution
~# ## -- Add 14th argument (3633) for Milwaukee work trip distribution --
~<macros/call/amhwIOM_H.mac 02 02 32 33 37 67 05 06 68 69 %ms98% %1% 3632 3633
~/
~/  home-other distribution
~/
~<macros/call/ophoIOM.mac 03 03 30 31 38 67 05 06 68 69 %ms98% %1% 3632
~/
~/  nonhome distribution
~/
~<macros/call/opnhIOM.mac 04 04 28 29 39 67 05 06 68 69 %ms98% %1% 3632
~/
~/**********************************************************************
~/
~/   List matrices after completing three IOM trip purpose runs.
~/
~/*********************************************************************
~/
reports=report\iter_%ms98%\mflist.sim_%ms98%.rxt
~/
3.12
6
m
2
q
~/
~#
~+;~?x=2;~$>Milw_cleanup
~$>skip
~#
~:Milw_cleanup
~# ## -- RESET MATRICES TO THEIR ORIGINAL STATE
~# ## -- REMOVE X-COORD,Y-COORD,SQ MILES FOR ZONE 3633
3.21
~+;1;y;mo11;n;0; ; ;y;3633; ;1;
~+;1;y;mo12;n;0; ; ;y;3633; ;1;
~+;1;y;md12;n;0; ; ;y;3633; ;1;q
~#
~#
~:skip
~/**********************************************************************
~/
~/    End of macro - reset reports, then quit and return to emme prompt
~/
~/**********************************************************************
~/
reports=reports
~o=6
q
~/
~/**********************************************************************
