~#
~#**********************************************************************
~#********************************************************************** 
~#
~#  TOLL_MODE_CHOICE.MAC %1% %2%
~#
~#  where %1% = three digit alternative ID number
~#        %2% = model iteration for calculations
~#
~#  This is a macro that recalculates the mode choice probabilities 
~#  considering tolls accumulated along paths.  It basically reproduces
~#  the auto submode choices imbedded in the CMAP mode choice model.
~#
~#  It assumes that a path based assignment has been completed and 
~#  that the paths are available.  Toll links and tolls are included
~#  in the network and the model steps are the same as in the full
~#  blown model run.  A toll VDF is used and trips have adjusted
~#  route choices but not mode choices. 
~#  
~#  The logic of the macro is as follows:
~#
~#    1.  Identify the person movements traveling tolled paths by 
~#        auto occupancy.
~#    2.  Sum and then average the tolls along the used toll paths.
~#    3.  Calculate the change in mode choice utility due to tolls by:
~#        a.  trip purpose, work trips and am paths, non-work and 
~#            midday paths.
~#        b.  occupancy. 
~#    4.  Adjust the binary auto-transit mode choice probabilites.
~#    5.  Recalculate auto-transit binary mode choice.
~#    6.  Adjust the drive alone-shared ride probabilities.
~#    7.  Recalculate the drive alone-share ride mode choice.
~#    8.  Adjust the two and three plus auto occupancy probabilities
~#    9.  Recalculate the two and three plus auto occupancy mode shares.
~#
~#********************************************************************** 
~#
~#  Initial version by RWE, February 2012, Revised June 2012.
~# 
~#  Revisions:
~#     - Feb.- Apr. 2015 by Craig Heither: 
~#            * matrix initialization code moved to init_HOVsim_databk.mac;
~#            * path analysis modified for 6 vehicle classes
~#            * HO/NH path analyses call occupancy matrices r/t mf2 & factoring
~#     - Oct. 2018 by Craig Heither: 
~#            * some calculations are specific to the CBD - replaced hard-coded 
~#              maximum zone number with y-register; revised code to stop continually
~#              entering and exiting module 3.21
~#     - Apr. 2021 by Jason Lemp: 
~#            * removed skimming procedures as we are testing a new macro to generate
~#              toll skims, which will be used by the new model.
~#
~#********************************************************************** 
~#
~t4=report\iter_%ms98%\mc_toll_diversion.rpt
~#
reports=%t4%
~z=%2%
~z-1
s=%1%%z%3
~y=47              ~/ maximum CBD zone number - same for zone09 and zone17
~o|39
~#
~#********************************************************************** 
~#
~#  Calculate change in utility due to tolls
~#
~#**********************************************************************
~#
~#  Home-work, low income
~#   -- Shared ride 3 or more persons
~#
3.21
~+;1;yes;mf155;no;-0.00275*mf113/3.0;;;no;2
~#
~#   -- Shared ride 2 persons
~#
~+;1;yes;mf154;no;-0.00275*mf112/2.0;;;no;2
~#
~#   -- Shared ride composite
~#
~+;1;yes;mf153;no
((mf52+mf51).gt.0)*
  ((exp(mf155)*mf52/(mf52+mf51))+(exp(mf154)*mf51/(mf52+mf51)))
~+;;;no;2
~+;1;yes;mf153;no;((mf52+mf51).gt.0)*ln(mf153);;;no;2
~+;1;yes;mf153;no;0.536*mf153;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf152;no;-0.001155*mf111;;;no;2
~#
~#   -- Auto composite
~#
~+;1;yes;mf151;no
((mf52+mf51+mf50).gt.0)*
  ((exp(mf153)*(mf52+mf51)/(mf52+mf51+mf50))+
  (exp(mf152)*mf50/(mf52+mf51+mf50)))
~+;;;no;2
~+;1;yes;mf151;no;((mf52+mf51+mf50).gt.0)*ln(mf151);;;no;2
~+;1;yes;mf151;no;0.571*mf151;;;no;2
~#
~#  ------------------------------------------------
~#  Home-work, high income
~#   -- Shared ride 3 or more persons
~#
~+;1;yes;mf160;no;-0.00275*mf116/3.0;;;no;2
~#
~#   -- Shared ride 2 persons
~#
~+;1;yes;mf159;no;-0.00275*mf115/2.0;;;no;2
~#
~#   -- Shared ride composite
~#
~+;1;yes;mf158;no
((mf55+mf54).gt.0)*
  ((exp(mf160)*mf55/(mf55+mf54))+(exp(mf159)*mf54/(mf55+mf54)))
~+;;;no;2
~+;1;yes;mf158;no;((mf55+mf54).gt.0)*ln(mf158);;;no;2
~+;1;yes;mf158;no;0.536*mf158;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf157;no;-0.001155*mf114;;;no;2
~#
~#   -- Auto composite
~#
~+;1;yes;mf156;no
((mf55+mf54+mf53).gt.0)*
  ((exp(mf158)*(mf55+mf54)/(mf55+mf54+mf53))+
  (exp(mf157)*mf53/(mf55+mf54+mf53)))
~+;;;no;2
~+;1;yes;mf156;no;((mf55+mf54+mf53).gt.0)*ln(mf156);;;no;2
~+;1;yes;mf156;no;0.571*mf156;;;no;2
~#
~#  ------------------------------------------------
~#  Home-other
~#
~#   -- Get occupancy-specific shares for purpose --
~#    --  Rather than hard-coding factors, let Emme calculate on-the-fly --
~+;1;y;ms40;y;hotot;total HO auto person trips;~?q=1;y;0;mf2; ; ;n;+;+;2
~+;1;y;ms41;y;hosov;total HO sov auto person trips;~?q=1;y;0;mf104; ; ;n;+;+;2
~+;1;y;ms42;y;hohv2;total HO hov2 auto person trips;~?q=1;y;0;mf105; ; ;n;+;+;2
~+;1;y;ms43;y;hohv3;total HO hov3 auto person trips;~?q=1;y;0;mf106; ; ;n;+;+;2
~+;1;y;ms44;y;hosh1;HO sov auto person trips share;~?q=1;y;0;ms41/ms40; ; ;2
~+;1;y;ms45;y;hosh2;HO hov2 auto person trips share;~?q=1;y;0;ms42/ms40; ; ;2
~+;1;y;ms46;y;hosh3;HO hov3 auto person trips share;~?q=1;y;0;ms43/ms40; ; ;2
~#
~#   -- Shared ride 3 or more
~#
~+;1;yes;mf163;no
(q.le.%y%)*(-0.0015*mf119/3.0)+
  (q.gt.%y%)*(-0.0059*mf119/3.0)
~+;;;no;2
~#
~#   -- Shared ride 2 persons
~#
~+;1;yes;mf162;no
(q.le.%y%)*(-0.0015*mf118/2.0)+
  (q.gt.%y%)*(-0.0059*mf118/2.0)
~+;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf161;no
(q.le.%y%)*(-0.0015*mf117)+
  (q.gt.%y%)*(-0.0059*mf117)
~+;;;no;2
~#
~#   -- Auto (auto occupancy probabilities/weights from triptable macro)
~#
~+;1;yes;mf167;no
%ms46%*exp(mf163)+%ms45%*exp(mf162)+%ms44%*exp(mf161)
~+;;;no;2
~+;1;yes;mf167;no;ln(mf167);;;no;2
~#
~#   -- Shared ride composite - [CMH]
~#
~+;1;yes;mf201;no
((mf106+mf105).gt.0)*
  ((exp(mf163)*mf106/(mf106+mf105))+(exp(mf162)*mf105/(mf106+mf105)))
~+;;;no;2
~+;1;yes;mf201;no;((mf106+mf105).gt.0)*ln(mf201);;;no;2
~#
~#  ------------------------------------------------
~#  Nonhome
~#
~#   -- Get occupancy-specific shares for purpose --
~#    --  Rather than hard-coding factors, let Emme calculate on-the-fly --
~#
~+;1;y;ms47;y;nhtot;total NH auto person trips;~?q=1;y;0;mf3; ; ;n;+;+;2
~+;1;y;ms48;y;nhsov;total NH sov auto person trips;~?q=1;y;0;mf107; ; ;n;+;+;2
~+;1;y;ms49;y;nhhv2;total NH hov2 auto person trips;~?q=1;y;0;mf108; ; ;n;+;+;2
~+;1;y;ms50;y;nhhv3;total NH hov3 auto person trips;~?q=1;y;0;mf109; ; ;n;+;+;2
~+;1;y;ms51;y;nhsh1;NH sov auto person trips share;~?q=1;y;0;ms48/ms47; ; ;2
~+;1;y;ms52;y;nhsh2;NH hov2 auto person trips share;~?q=1;y;0;ms49/ms47; ; ;2
~+;1;y;ms53;y;nhsh3;NH hov3 auto person trips share;~?q=1;y;0;ms50/ms47; ; ;2
~#
~#   -- Shared ride 3 or more
~#
~+;1;yes;mf166;no
(q.le.%y%)*(-0.0015*mf122/3.0)+
  (q.gt.%y%)*(-0.0059*mf122/3.0)
~+;;;no;2
~#
~#   -- Shared ride 2 persons
~#
~+;1;yes;mf165;no
(q.le.%y%)*(-0.0015*mf121/2.0)+
  (q.gt.%y%)*(-0.0059*mf121/2.0)
~+;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf164;no
(q.le.%y%)*(-0.0015*mf120)+
  (q.gt.%y%)*(-0.0059*mf120)
~+;;;no;2
~#
~#   -- Auto (auto occupancy probabilities/weights from triptable macro)
~#
~+;1;yes;mf168;no
%ms53%*exp(mf166)+%ms52%*exp(mf165)+%ms51%*exp(mf164)
~+;;;no;2
~+;1;yes;mf168;no;ln(mf168);;;no;2
~#   -- Shared ride composite - [CMH]
~#
~+;1;yes;mf202;no
((mf109+mf108).gt.0)*
  ((exp(mf166)*mf109/(mf109+mf108))+(exp(mf165)*mf108/(mf109+mf108)))
~+;;;no;2
~+;1;yes;mf202;no;((mf109+mf108).gt.0)*ln(mf202);;;no;2
~#
~#********************************************************************** 
~#
~#  Calculate new probabilities for toll movements
~#
~#********************************************************************** 
~#
~#  Home-work, low income
~#
~#   -- Auto-transit
~#
~+;1;yes;mf171;no
exp(mf151)*mf48/(exp(mf151)*mf48+mf40)
~+;;mf48;;no;2
~#
~#   -- Drive alone-ride share
~#
~+;1;yes;mf172;no
exp(mf152)*mf50/(exp(mf152)*mf50+exp(mf153)*(mf51+mf52))
~+;;mf50;;no;2
~#
~+;1;yes;mf173;no;1.00-mf172;;;no;2
~#
~#   -- Two person-three or more persons ride share
~#
~+;1;yes;mf174;no
exp(mf154)*mf51/(exp(mf154)*mf51+exp(mf155)*(mf52))
~+;;mf51;;no;2
~#
~+;1;yes;mf175;no;1.00-mf174;;;no;2
~#
~#  ------------------------------------------------
~#  Home-work, high income
~#
~#   -- Auto-transit
~#
~+;1;yes;mf176;no
exp(mf156)*mf49/(exp(mf156)*mf49+mf41)
~+;;mf49;;no;2
~#
~#   -- Drive alone-ride share
~#
~+;1;yes;mf177;no
exp(mf157)*mf53/(exp(mf157)*mf53+exp(mf158)*(mf54+mf55))
~+;;mf53;;no;2
~#
~+;1;yes;mf178;no;1.00-mf177;;;no;2
~#
~#   -- Two person-three or more persons ride share
~#
~+;1;yes;mf179;no
exp(mf159)*mf54/(exp(mf159)*mf54+exp(mf160)*(mf55))
~+;;mf54;;no;2
~#
~+;1;yes;mf180;no;1.00-mf179;;;no;2
~#
~#  ------------------------------------------------
~#  Home-other
~#
~#   -- Auto-transit
~#
~+;1;yes;mf181;no
exp(mf167)*mf2/(exp(mf167)*mf2+mf42)
~+;;mf2;;no;2
~#
~#   -- [section expanded to account for separate vehicle occupancy matrices] --
~#
~#   -- Drive alone-ride share
~#
~# # -- (.GT.0) Logic Added to Prevent Division by Zero -- 
~+;1;yes;mf182;no
((mf104+mf105+mf106).gt.0)*
  (exp(mf161)*mf104/(exp(mf161)*mf104+exp(mf201)*(mf105+mf106)))
~+;;mf104;;no;2
~#
~+;1;yes;mf183;no;1.00-mf182;;;no;2
~#
~#   -- Two person-three or more persons ride share
~#
~+;1;yes;mf184;no
exp(mf162)*mf105/(exp(mf162)*mf105+exp(mf163)*(mf106))
~+;;mf105;;no;2
~#
~+;1;yes;mf185;no;1.00-mf184;;;no;2
~#
~#  ------------------------------------------------
~#  Nonhome
~#
~#  Auto-transit
~#
~+;1;yes;mf186;no
exp(mf168)*mf3/(exp(mf168)*mf3+mf43)
~+;;mf3;;no;2
~#
~#   -- [section expanded to account for separate vehicle occupancy matrices] --
~#
~#   -- Drive alone-ride share
~#
~# # -- (.GT.0) Logic Added to Prevent Division by Zero -- 
~+;1;yes;mf187;no
((mf107+mf108+mf109).gt.0)*
  (exp(mf164)*mf107/(exp(mf164)*mf107+exp(mf202)*(mf108+mf109)))
~+;;mf104;;no;2
~#
~+;1;yes;mf188;no;1.00-mf187;;;no;2
~#
~#   -- Two person-three or more persons ride share
~#
~+;1;yes;mf189;no
exp(mf165)*mf108/(exp(mf165)*mf108+exp(mf166)*(mf109))
~+;;mf108;;no;2
~#
~+;1;yes;mf190;no;1.00-mf189;;;no;2
~#
~#********************************************************************** 
~#
~#  Calculate delta trip interchanges
~#
~#********************************************************************** 
~#
~#  Home-work, low income
~#
~#   -- Auto person
~#
~+;1;yes;mf191;no
(mf48.gt.0)*mf171*((mf48+mf40)/mf48)*(mf131+mf132+mf133)
~+;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf192;no;mf172*mf191;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;yes;mf193;no;mf173*mf174*mf191;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;yes;mf194;no;mf173*mf175*mf191;;;no;2
~#
~#  ------------------------------------------------
~#  Home-work, high income
~#
~#   -- Auto person
~#
~+;1;yes;mf195;no
(mf49.gt.0)*mf176*((mf49+mf41)/mf49)*(mf134+mf135+mf136)
~+;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf196;no;mf177*mf195;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;yes;mf197;no;mf178*mf179*mf195;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;yes;mf198;no;mf178*mf180*mf195;;;no;2
~#
~#  ------------------------------------------------
~#  Home-other
~#
~#   -- Auto person
~#
~+;1;yes;mf199;no
(mf2.gt.0)*mf181*((mf2+mf42)/mf2)*(mf137+mf138+mf139)
~+;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf200;no;mf182*mf199;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;yes;mf203;no;mf183*mf184*mf199;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;yes;mf204;no;mf183*mf185*mf199;;;no;2
~#
~#  ------------------------------------------------
~#  Nonhome
~#
~#   -- Auto person
~#
~+;1;yes;mf205;no
(mf3.gt.0)*mf186*((mf3+mf43)/mf3)*(mf140+mf141+mf142)
~+;;;no;2
~#
~#   -- Drive alone
~#
~+;1;yes;mf206;no;mf187*mf205;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;yes;mf207;no;mf188*mf189*mf205;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;yes;mf208;no;mf188*mf190*mf205;;;no;2
~#
~#********************************************************************** 
~#
~#  Calculate final new trip tables adjusted for tolls
~#
~#********************************************************************** 
~#
~#  Home-work, low income
~#   -- Transit home-work low income
~#
~+;1;n;mf40; ; ;n; ; ;2
~+;1;yes;mf40;no;mf40+(mf131+mf132+mf133)-mf191;;;no;2
~#
~#   -- Auto home-work, low income
~#
~+;1;n;mf48; ; ;n; ; ;2
~+;1;yes;mf48;no;mf48-(mf131+mf132+mf133)+(mf192+mf193+mf194);;;no;2
~#
~#   -- Drive alone, low income
~#
~+;1;n;mf50; ; ;n; ; ;2
~+;1;yes;mf50;no;mf50-mf131+mf192;;;no;2
~#
~#   -- HOV 2 person, low income
~#
~+;1;n;mf51; ; ;n; ; ;2
~+;1;yes;mf51;no;mf51-mf132+mf193;;;no;2
~#
~#   -- HOV 3+ person, low income
~#
~+;1;n;mf52; ; ;n; ; ;2
~+;1;yes;mf52;no;mf52-mf133+mf194;;;no;2
~#
~#  ------------------------------------------------
~#  Home-work, high income
~#   -- Transit home-work high income
~#
~+;1;n;mf41; ; ;n; ; ;2
~+;1;yes;mf41;no;mf41+(mf134+mf135+mf136)-mf195;;;no;2
~#
~#   -- Auto home-work, high income
~#
~+;1;n;mf49; ; ;n; ; ;2
~+;1;yes;mf49;no;mf49-(mf134+mf135+mf136)+(mf196+mf197+mf198);;;no;2
~#
~#   -- Drive alone
~#
~+;1;n;mf53; ; ;n; ; ;2
~+;1;yes;mf53;no;mf53-mf134+mf196;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;n;mf54; ; ;n; ; ;2
~+;1;yes;mf54;no;mf54-mf135+mf197;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;n;mf55; ; ;n; ; ;2
~+;1;yes;mf55;no;mf55-mf136+mf198;;;no;2
~#
~#  ------------------------------------------------
~#  Home-Other
~#   -- Transit home-other
~#
~+;1;n;mf42; ; ;n; ; ;2
~+;1;yes;mf42;no;mf42+(mf137+mf138+mf139)-mf199;;;no;2
~#
~#   -- Auto home-other
~#
~+;1;n;mf2; ; ;n; ; ;2
~+;1;yes;mf2;no;mf2-(mf137+mf138+mf139)+(mf200+mf203+mf204);;;no;2
~#
~#   -- Drive alone
~#
~+;1;n;mf104; ; ;n; ; ;2
~+;1;yes;mf104;no;mf104-mf137+mf200;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;n;mf105; ; ;n; ; ;2
~+;1;yes;mf105;no;mf105-mf138+mf203;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;n;mf106; ; ;n; ; ;2
~+;1;yes;mf106;no;mf106-mf139+mf204;;;no;2
~#
~#  ------------------------------------------------
~#  Nonhome
~#   -- Transit nonhome
~#
~+;1;n;mf43; ; ;n; ; ;2
~+;1;yes;mf43;no;mf43+(mf140+mf141+mf142)-mf205;;;no;2
~#
~#   -- Auto nonhome
~#
~+;1;n;mf3; ; ;n; ; ;2
~+;1;yes;mf3;no;0.max.(mf3-(mf140+mf141+mf142)+(mf206+mf207+mf208));;;no;2
~#
~#   -- Drive alone
~#
~+;1;n;mf107; ; ;n; ; ;2
~+;1;yes;mf107;no;mf107-mf140+mf206;;;no;2
~#
~#   -- HOV 2 person
~#
~+;1;n;mf108; ; ;n; ; ;2
~+;1;yes;mf108;no;mf108-mf141+mf207;;;no;2
~#
~#   -- HOV 3+ person
~#
~+;1;n;mf109; ; ;n; ; ;2
~+;1;yes;mf109;no;mf109-mf142+mf208;;;no;2
~#
~#
~# ########### Synchronize Matrix Values to Ensure Consistency ###########
~+;1;y;mf101;y;sov1w%ms98%;hw SOV person trips sim_%ms98%-total;~?q=1;y;0;mf50+mf53; ; ;n;2
~+;1;y;mf102;y;hov2w%ms98%;hw HOV2 person trips sim_%ms98%-total;~?q=1;y;0;mf51+mf54; ; ;n;2
~+;1;y;mf103;y;hov3w%ms98%;hw HOV3+ person trips sim_%ms98%-total;~?q=1;y;0;mf52+mf55; ; ;n;2;q
~#
~#
~#
~#
~>>%t4%
~" 
~" 
~" ===========================================================================================
~"   ***********************    -- VERIFY MATRIX TOTALS MATCH --    ***********************
~" ===========================================================================================
~>>
~#
3.21
~+;1;n;mf101-(mf50+mf53); ; ;n;+;+;2
~+;1;n;mf102-(mf51+mf54); ; ;n;+;+;2
~+;1;n;mf103-(mf52+mf55); ; ;n;+;+;2
~+;1;n;mf48-(mf50+mf51+mf52); ; ;n;+;+;2
~+;1;n;mf49-(mf53+mf54+mf55); ; ;n;+;+;2
~+;1;n;mf2-(mf104+mf105+mf106); ; ;n;+;+;2
~+;1;n;mf3-(mf107+mf108+mf109); ; ;n;+;+;2
q
~#
~#
~#  ------------------------------------------------
~#  -- delete scalars --
~x=40
~:del_scalar
~+;3.12;2;ms%x%;y;q
~x+1
~+;~?x<54;~$del_scalar
~#  ------------------------------------------------
~#
~#  The end
~#
~o=6
reports=
~:end
q