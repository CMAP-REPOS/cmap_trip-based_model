~#
~/**********************************************************************
~/**********************************************************************
~#
~/  Macro TOD_skim_setup.mac %1% %2%
~/   Where %1% = three digit alternative ID number.
~/         %2% = time period number
~#
~/**********************************************************************
~/  Macro written to create time skims for each time period.
~/**********************************************************************
~/  Time and distance skims are prepared for the following time periods 
~/    and stored in these full matricies.
~#
~/    mf461 = overnight 8p-6a SOV hwy time skim
~/    mf462 = am pre-pk shoulder 6a-7a SOV hwy time skim
~/    mf463 = am peak 7a-9a SOV time skim
~/    mf464 = am post-pk shoulder 9a-10a SOV hwy time skim
~/    mf465 = midday 10a-2p SOV hwy time skim
~/    mf466 = pm pre-pk shoulder 2p-4p SOV hwy time skim
~/    mf467 = pm peak 4p-6p SOV hwy time
~/    mf468 = pm post-pk shoulder 6p-8p SOV hwy time skim
~/
~/    ms97 = dummy operand for demand matrix in assignment setup (5.11)
~/
~/    extra function parameter (el1) used in averaging vdfs
~#
~/**********************************************************************
~/  Set reports and batchin
~/**********************************************************************
reports=report\iter_%ms98%\skimtod_iter_%s%.rxt
~o|39
~x=%ms98%
batchin=data\vdf_iter.in
~#
~/********************************************************************** 
~#
~/  Use averaging volume delay functions for skimming.
~/    Delete existing functions and batch in new functions.
~#
~/**********************************************************************
~+;4.12;4;fd1,fd10; ;~?e;~$>err;y
~:err
q
~#
4.11
2
~#
~/**********************************************************************
~/  Make temp copy of scenarios for skimming.
~/**********************************************************************
~#
~z=%s%
~# ## -- Delete temp scenario if it exists -- ##
1.22
2
90
~?e
~+; ;q;~$>next
yes
q
~:next
~# ## -- Copy scenario -- ##
1.22
3
%z%
90
temp scenario %2% for skimming
no
q
~#
~/**********************************************************************
~/  Set scenario.
~/**********************************************************************
~x=90
~#
s=%x%
~/**********************************************************************
~/  Set period text registers.
~/**********************************************************************
~# -- SOV --
~r1=46%2%
~t1=todtime%2%
~t2=SOV hwy time skim period %2%
~#
~#
~/**********************************************************************
~/  Initialize time matrix
~/**********************************************************************
~#
3.12
2
mf%r1%
~?e
~$>err
yes
~$>end
~:err

r
~:end
~+; 1; mf%r1%; %t1%; %t2%; 0.0
~#
q
~#
~/**********************************************************************
~/  Sum class average volumes into new extra attrib @avtot
~/**********************************************************************
2.42
3
@avtot
~?e
~$>err
yes
q
~$>end
~:err

q
~:end
~/
2.42
2
2
@avtot
total average volume (veq)
0
q
2.41
1
yes
@avtot
no
@avs1v+@avs2v+@avs3v+@avh2v+@avh3v+@avbqv+@avlqv+@avmqv+@avhqv

all
2
2
q
~#
~#
el1=@avtot
~/**********************************************************************
reports=
batchin=
~/**********************************************************************
q
