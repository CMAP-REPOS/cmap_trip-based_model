~/
reports=report\iter_%ms98%\ttables_%s%.rxt
s=%1%%ms98%%2%
~/**********************************************************************
~/   Bring in TOD volume delay functions.
~/**********************************************************************
~/
~/ delete current functions
~+;4.12;4;fd1,fd10; ;~?e;~$>err;y
~:err
q
~/
batchin=data\vdf_tod.in
~+;4.11;2;
~/
~t3=report\iter_%ms98%\tod_veh_trips_and_VOT.txt
~#
~o|39
~/**********************************************************************
~/**********************************************************************
~/      ttables %1% %2% %3% %4%
~/   Where %1% = three digit alternative ID number
~/         %2% = TOD period
~/         %3% = 13 if HOV2 vehicles are class S
~/             = 92 if HOV2 vehicles are class H
~/         %4% = 13 if HOV3 vehicles are class S
~/             = 93 if HOV3 vehicles are class H
~/
~/   Macro to prepare period %2% trip tables
~/   This version prepares six vehicle class tables where class H
~/    (mf18) can be zero depending on %3% and %4% parameter values.
~/
~/           Revised by DBE Dec2002 for Mobile6 changes
~/           Revised by DBE 18Sep2003 for ORD adjustment
~/           Revised by DBE 19Nov2003 for put/get
~/           Revised by DBE 20Apr2004 for 5 classes
~/           Revised by DBE 1OCT2004 for fulliter batch
~/           Revised by DBE 17nov2004 to use replaceable parameter %1%
~/            in reports path.
~/
~#        Heither, 06/21/2010 - park-and-ride trips included in TOD auto
~#                              demand table. Matrix convolution performed
~#                              to extract first leg demand using indexed
~#                              station zone.
~#
~#        Heither, 03/10/2011 - corrected & renamed time period matrix labels.
~#
~#        Heither, 12/06/2012 - detailed TOD trip purpose demand written to report.
~#
~#        Heither, 06/14/2013 - calls new kzone file created by renumber.transit.skims.
~#
~#        Heither, 10/30/2014 - matrix values adjusted for non-work HOV procedures.
~#
~#        Ferguson, 02/10/2015 - change from 6 classes to 7 classes by dividing HOV into HOV2 and HOV3+
~# 
~#        Ferguson, 04/29/2015 - tod factor registers renumbered to accommodate expanded SOV and HOV factors.
~#	      Bozic 12/15/2015 - revised to reference existing peak hour index matrix instead of batching in
~#	                     and do not delete mf"kzonep" afterwards 11-2-2017 changed to kasmpt assignment index matrix
~#
~#        Heither, 07-15-2016 - implements VOT calculations for toll generalized cost.
~#
~#        Heither, 09-02-2016 - User Class toll perception factors included in VOT calculations for toll generalized cost.
~#        
~#        Bozic, 4-14-2017- add separate matrix mf848 milwaukee trips to mf8
~#        Ferguson, 9/18/2018 - updated r250 to number of zone17 zones
~#        JLemp, 07-07-2021 - implement new vehicle class framework (3 SOV classes, combined b-plate + light trucks)
~/**********************************************************************
~/**********************************************************************
~r248=%3%                       /* store matrix value for HOV2 trips
~r249=%4%                       /* store matrix value for HOV3+ trips
~r250=3632                      /* max. non-POE zone
~#
~/batchin zone flags for fractioning triptables
~/ add by kww 6/05 to ease shifting between zone systems
~/
batchin=data/tod_flags.in
3.11
1
q
batchin=batchin
3.12
~+;4;3;mo"pflag";md"qflag";y;tod fraction flags;0
q
~/ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~/ Get TOD Trip Table Factors
~/ CMH 06/08 - to eliminate hardcoding of factors in macros (see documentation in Excel file)
~/
~y=%2%
~# ## SOV factors for HBW to CBD (r1) & HBW from CBD (r3) -- NEED to Update
~+;~r1=0.122;~r3=0.168
~+;~?y=2;~r1=0.096;~r3=0.028
~+;~?y=3;~r1=0.318;~r3=0.069
~+;~?y=4;~r1=0.072;~r3=0.026
~+;~?y=5;~r1=0.154;~r3=0.130
~+;~?y=6;~r1=0.081;~r3=0.119
~+;~?y=7;~r1=0.088;~r3=0.271
~+;~?y=8;~r1=0.069;~r3=0.189
~# # r4 = Metra share of boardings - used to factor Park and Ride trips
~r4=0.126
~/ -- Get TOD Factors
~<<data\tod_factors.p%y%
~z=19
~:loop1
~+;~t0=~@;~?e;~$>eof1
~rz=%t0%
~/r%z%=%rz%
~z+1
~+;~?z<27;~$loop1
~:eof1
~/end of reading factors
~/
~/ -- Get TOD Occupancy Rates
~<<data\tod_occ.p%y%
~z=31
~:loop2
~+;~t0=~@;~?e;~$>eof2
~rz=%t0%
~/r%z%=%rz%
~z+1
~+;~?z<40;~$loop2
~:eof2
~/end of reading occ
~/
~# ~/ -- Get Directional Splits
~# ~<<data\directional.splits
~# ~z=41
~# ~:loop3
~# ~+;~t0=~@;~?e;~$>eof3
~# ~rz=%t0%
~# ~/r%z%=%rz%
~# ~z+1
~# ~+;~?z<57;~$loop3
~# ~:eof3
~# ~/end of reading splits
~# ~/
~/ Set Matrix labels
~t1=p1. 8pm-6am
~+;~?y=2;~t1=p2. 6am-7am
~+;~?y=3;~t1=p3. 7am-9am
~+;~?y=4;~t1=p4. 9am-10am
~+;~?y=5;~t1=p5. 10am-2pm
~+;~?y=6;~t1=p6. 2pm-4pm
~+;~?y=7;~t1=p7. 4pm-6pm
~+;~?y=8;~t1=p8. 6pm-8pm
~#
~/**********************************************************************
~/
~/   Initialize period trip tables mf13 - mf18 + mf92-mf97
~/
~/**********************************************************************
3.21
~+;1;y;mf14;y;bplt;b plate truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf15;y;ltrk;light truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf16;y;mtrk;medium truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf17;y;htrk;heavy truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf18;y;modeH;auto veh (mod=H) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf92;y;modeH2;auto veh (mod=H) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf93;y;modeH3;auto veh (mod=H) trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf94;y;modeS1;SOV VOT1 auto veh trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf95;y;modeS2;SOV VOT2 auto veh trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf96;y;modeS3;SOV VOT3 auto veh trip table:%t1%;~?q=1;y;0;0; ; ;n;1;
~+;1;y;mf97;y;bp_ltrk;b plate + light truck trips for %t1%;~?q=1;y;0;0; ; ;n;1;q
~#
~#**********************************************************************
~#  ## ---  Value-of-Time Data --- ##
~#**********************************************************************
~# ## -- Value of Time (dollars per hour) -- ##
~r100=2.3     / VOT for SOV VOT1
~r101=7.2     / VOT for SOV VOT2
~r102=24     / VOT for SOV VOT3
~r103=9      / VOT for HOV2 (prev value = 18 for hw low & non-hw, 30 for hw high)
~r104=14     / VOT for HOV3+ (prev value = 27 for hw low & non-hw, 45 for hw high)
~r109=24     / VOT for B-Plate truck
~r110=32     / VOT for Light truck
~r111=47     / VOT for Medium truck
~r112=52     / VOT for Heavy truck
~r113=18     / VOT for External auto
~r114=30     / VOT for AirPass
~# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - 
~# ## -- Vehicle Class Perception Factors -- ##
~#      Used to reflect that User Classes may not perceive 100% of the
~#      cost of a toll (effectively increases their VOT in toll generalized cost). 
~#      -  Commercial vehicle drivers may be reimbursed for tolls or their
~#         Just-in-Time delivery responsibilities may play a much larger role in
~#         route decisions than tolls. 
~#      -  Auto I-PASS users may not perceive the entire cost because they are not
~#         paying out-of-pocket for each transaction (i.e., only see monthly credit
~#         card statement).
~r100*2.25    / SOV VOT1
~r101*2.25    / SOV VOT2
~r102*2.00    / SOV VOT3
~r103*2.25    / HOV2 (prev value = 9.0/8.0)
~r104*2.25    / HOV3 (prev value = 9.0/8.0)
~r109*1.00    / B-Plate truck (prev value = 2.0)
~r110*1.00    / B-plate + Light truck (prev value = 2.0)
~r111*1.00    / Medium truck (prev value = 2.0)
~r112*1.00    / Heavy truck (prev value = 2.0)
~r113*1.00    / External auto (prev value = 3.0)
~r114*1.00    / AirPass (prev value = 3.0)
~#**********************************************************************
~#
~# VOT bucket splits for added trip tables (externals, drive-transit, air passenger)
~r61=0.10     / VOT1 share of external trips
~r62=0.45     / VOT2 share of external trips
~r63=0.45     / VOT3 share of external trips
~r64=0.15     / VOT1 share of drive-transit trips
~r65=0.50     / VOT2 share of drive-transit trips
~r66=0.35     / VOT3 share of drive-transit trips
~r67=0.10     / VOT1 share of air passenger trips
~r68=0.45     / VOT2 share of air passenger trips
~r69=0.45     / VOT3 share of air passenger trips
~#**********************************************************************
~#
~# Vehicle trip matrix id's from Demand model
~r81=41%y%     / SOV VOT1 vehicle trips
~r82=42%y%     / SOV VOT2 vehicle trips
~r83=43%y%     / SOV VOT3 vehicle trips
~r84=44%y%     / HOV(2) vehicle trips
~r85=45%y%     / HOV3+ vehicle trips
~/ Demand Matrix IDs = %r81%,%r82%,%r83%,%r84%,%r85%
~#**********************************************************************
~#
~#
~+;~?y>1;~$>not_TOD1
~+;3.21;1;y;mf59;y;parkrd;drive to transit station;~?q=1;y;0;0; ; ;n;1;q
~/
~/ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~#
~# ## PERFORM CONVOLUTION TO GET TRIPS FROM ORIGIN ZONE TO STATION ZONE ##
~# ## ONLY NECESSARY ONCE - DURING TOD 1 ##
~+;~?y>1;~$>not_TOD1
~#
~#
~# -- create combined HW transit demand matrix (high + low income) --
~# -- These matrices are in O-D format --
3.21
~+;1;y;mf20;y;hwtranst;HW High and Low inc transit trips;~?q=1;y;0;mf40+mf41; ; ;n;2
~#
~# -- create matrix of origins for convolution --
~+;1;y;mo10;~?q=1;y;ptemp;origins to extract first leg demand;0;p; ; ;y;1,%r250%; ;2;q
~#
~# -- perform convolution --
3.23
1
mf837                    ~/first operand matrix
==                       ~/matrix combination operator
mo10                     ~/second operand matrix
y                        ~/use transpose of second operand matrix?
*                        ~/masking operator 1
n                        ~/same masking operator for all intermed. zones?
mf20                     ~/matrix containing masking value
2                        ~/origin to intermed. zone index
 
 
mf59                     ~/results matrix
n                        ~/change header?
y                        ~/submatrix?
1,%r250%                 ~/origin zones
 
1,%r250%                 ~/intermed. zones
 
1,%r250%                 ~/destinations


y
2
q
~#
~# -- delete temporary matrices --
3.12
~+;2;mf20;y
~+;2;mo10;y;q
~# 
~:not_TOD1
~/ * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
~/**********************************************************************
~/
~/    Calculate mf94-mf96 as time period mode S (VOT1,VOT2,VOT3) auto vehicles and if mode H
~/      assignment desired mf18 mode H auto vehicles:
~/      convert/combine hbw person trips by occupancy level from mode
~/      choice to auto vehicle trips
~/    modified to read zone flag array rather than hard coded zones, kww 6/05
~/
~/**********************************************************************
3.21
1
yes
mf94
no
~/  SOV VOT1 (mf%r81%)
mf%r81%


no
2
~/
~# ## 
~+;1;y;ms24;y;tmp24;store SOV VOT1;~?q=1;y;0;mf94; ; ;n;+;+;1;q
~t2=SOV VOT1 trips
~>>%t3%
~"Period %y% %t2% : %ms24.2%
~>>
3.21
1
yes
mf95
no
~/  SOV VOT2 (mf%r82%)
mf%r82%


no
2
~/
~# ## 
~+;1;y;ms25;y;tmp25;store SOV VOT2;~?q=1;y;0;mf95; ; ;n;+;+;1;q
~t2=SOV VOT2 trips
~>>%t3%
~"Period %y% %t2% : %ms25.2%
~>>
3.21
1
yes
mf96
no
~/  SOV VOT3 (mf%r83%)
mf%r83%


no
2
~/
~# ## 
~+;1;y;ms26;y;tmp26;store HW SOV VOT3;~?q=1;y;0;mf96; ; ;n;+;+;1;q
~t2=SOV VOT3 trips
~>>%t3%
~"Period %y% %t2% : %ms26.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r248%
no
~/  HOV2 (mf%r84%)
mf%r84%


no
2
~/
~# ## 
~+;1;y;ms27;y;tmp27;store HOV2;~?q=1;y;0;mf%r248%; ; ;n;+;+;1;q
~t2=HOV2 trips
~>>%t3%
~"Period %y% %t2% : %ms27.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf%r249%
no
~/  HOV3+ (mf%r85%)
mf%r85%


no
2
~/
~# ## 
~+;1;y;ms28;y;tmp28;store HOV3;~?q=1;y;0;mf%r249%; ; ;n;+;+;1;q
~t2=HOV3 trips
~>>%t3%
~"Period %y% %t2% : %ms28.2%
~>>
~/**********************************************************************
~/
~/    Combine HOV2 & HOV3+
~/
~/**********************************************************************
3.21
~+;1;y;mf18;n;mf%r248%+mf%r249%; ; ;n;1;q
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add in externals to mf94 to mf96
~/
~/**********************************************************************
~#
1
yes
mf94
no
mf94+
~/  External
((mf08+mf848)*%r19%*%r61%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms21;y;tmp21;store External VOT1;~?q=1;y;0;((mf08+mf848)*%r19%*%r61%); ; ;n;+;+;1;q
~t2=External SOV VOT1 trips
~>>%t3%
~"Period %y% %t2% : %ms21.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf95
no
mf95+
~/  External
((mf08+mf848)*%r19%*%r62%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms22;y;tmp22;store External VOT2;~?q=1;y;0;((mf08+mf848)*%r19%*%r62%); ; ;n;+;+;1;q
~t2=External SOV VOT2 trips
~>>%t3%
~"Period %y% %t2% : %ms22.2%
~>>
3.21
~# ## 
~# ## 
1
yes
mf96
no
mf96+
~/  External
((mf08+mf848)*%r19%*%r63%)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms23;y;tmp23;store External VOT3;~?q=1;y;0;((mf08+mf848)*%r19%*%r63%); ; ;n;+;+;1;q
~t2=External SOV VOT3 trips
~>>%t3%
~"Period %y% %t2% : %ms23.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add 
~/               air passenger rows and columns
~/    modified to read zone flag array rather than hard coded zones, kww 6/05
~/
~/**********************************************************************
1
yes
mf94
no
mf94+
mf10*("qflag".ge.2)*%r20%*%r67%+
mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%*%r67%


no
2
~/
~# ## 
~# ## 
~+;1;y;ms18;y;tmp18;store POE airport VOT1 trips;~?q=1;y;0;(mf10*("qflag".ge.2)*%r20%*%r67%)+(mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%*%r67%); ; ;n;+;+;1;q
~t2=POE Airport VOT1 trips
~>>%t3%
~"Period %y% %t2% : %ms18.2%
~>>
3.21
1
yes
mf95
no
mf95+
mf10*("qflag".ge.2)*%r20%*%r68%+
mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%*%r68%


no
2
~/
~# ## 
~# ## 
~+;1;y;ms19;y;tmp19;store POE airport VOT2 trips;~?q=1;y;0;(mf10*("qflag".ge.2)*%r20%*%r68%)+(mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%*%r68%); ; ;n;+;+;1;q
~t2=POE Airport VOT2 trips
~>>%t3%
~"Period %y% %t2% : %ms19.2%
~>>
3.21
1
yes
mf96
no
mf96+
mf10*("qflag".ge.2)*%r20%*%r69%+
mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%*%r69%


no
2
~/
~# ## 
~# ## 
~+;1;y;ms20;y;tmp20;store POE airport VOT3 trips;~?q=1;y;0;(mf10*("qflag".ge.2)*%r20%*%r69%)+(mf10*("pflag".ge.2)*("qflag".eq.0)*%r21%*%r69%); ; ;n;+;+;1;q
~t2=POE Airport VOT3 trips
~>>%t3%
~"Period %y% %t2% : %ms20.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Add 
~/      drive to transit station trips, Heither 06/2010
~/
~/**********************************************************************
1
yes
mf94
no
mf94+
mf59*%r64%*%r4%*(%r1%+%r3%)/2


no
2
~/
~# ## 
~# ## 
~+;1;y;ms15;y;tmp15;store P-n-R VOT1 trips;~?q=1;y;0;mf59*%r64%*%r4%*(%r1%+%r3%)/2; ; ;n;+;+;1;q
~t2=Park-and-Ride VOT1 trips
~>>%t3%
~"Period %y% %t2% : %ms15.2%
~>>
3.21
1
yes
mf95
no
mf95+
mf59*%r65%*%r4%*(%r1%+%r3%)/2


no
2
~/
~# ## 
~# ## 
~+;1;y;ms16;y;tmp16;store P-n-R VOT2 trips;~?q=1;y;0;mf59*%r65%*%r4%*(%r1%+%r3%)/2; ; ;n;+;+;1;q
~t2=Park-and-Ride VOT2 trips
~>>%t3%
~"Period %y% %t2% : %ms16.2%
~>>
3.21
1
yes
mf96
no
mf96+
mf59*%r66%*%r4%*(%r1%+%r3%)/2


no
2
~/
~# ## 
~# ## 
~+;1;y;ms17;y;tmp17;store P-n-R VOT3 trips;~?q=1;y;0;mf59*%r66%*%r4%*(%r1%+%r3%)/2; ; ;n;+;+;1;q
~t2=Park-and-Ride VOT3 trips
~>>%t3%
~"Period %y% %t2% : %ms17.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/  Prepare %t1% period truck trip tables
~/
~/**********************************************************************
~/
~/**********************************************************************
~/
~/    Calculate mf14 (as total b plate truck trips)
~/
~/**********************************************************************
1
yes
mf14
no
%r22%*mf04


no
2
~/
~# ## 
~# ## 
~+;1;y;ms14;y;tmp14;store b-plate trips;~?q=1;y;0;%r22%*mf04; ; ;n;+;+;1;q
~t2=B-Plate Truck trips
~>>%t3%
~"Period %y% %t2% : %ms14.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Calculate mf15, light truck trips
~/
~/**********************************************************************
1
yes
mf15
no
%r23%*mf05


no
2
~/
~# ## 
~# ## 
~+;1;y;ms13;y;tmp13;store light trk trips;~?q=1;y;0;%r23%*mf05; ; ;n;+;+;1;q
~t2=Light Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms13.2%
~>>
~/**********************************************************************
~/
~/    Combine b-plate and light trucks for assignment
~/
~/**********************************************************************
3.21
~+;1;y;mf97;n;mf14+mf15; ; ;n;1;q
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Calculate mf16, medium truck trips converted to veqs
~/
~/**********************************************************************
1
yes
mf16
no
%r24%*(2*mf06)


no
2
~/
~# ## 
~# ## 
~+;1;y;ms12;y;tmp12;store med trk trips;~?q=1;y;0;%r24%*(2*mf06); ; ;n;+;+;1;q
~t2=Medium Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms12.2%
~>>
3.21
~# ## 
~# ## 
~/**********************************************************************
~/
~/    Calculate mf17, heavy truck trips converted to veqs plus
~/      externals, which are assumed to be primarily heavy trucks
~/
~/**********************************************************************
1
yes
mf17
no
%r25%*(3*mf07)+
%r26%*(3*mf09)


no
2
~# ## 
~# ##
~+;1;y;ms10;y;tmp10;store heavy trk trips;~?q=1;y;0;%r25%*(3*mf07); ; ;n;+;+;1;
~+;1;y;ms11;y;tmp11;store POE Heavy trk trips;~?q=1;y;0;%r26%*(3*mf09); ; ;n;+;+;1;q
~t2=Heavy Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms10.2%
~>>
~t2=POE Heavy Duty Truck trips
~>>%t3%
~"Period %y% %t2% : %ms11.2%
~"- - - - - - - - - - - -
~>>
~# ## 
~/**********************************************************************
~# ## -- Calculations for VOT and Weight Factor -- ##
3.21
~# -- Toll Multipliers (Weight Factors) for Individual Components --
~+;1;y;ms29;y;tmp29;HOV2 toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r103%; ; ;2
~+;1;y;ms30;y;tmp30;HOV3 toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r104%; ; ;2
~+;1;y;ms31;y;tmp31;B-Plate trucks toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r109%; ; ;2
~+;1;y;ms32;y;tmp32;Light trucks toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r110%; ; ;2
~# -- Calculated Weighted VOT by Assignment User Class --
~+;1;y;ms33;y;tmp33;User Class 4 VOT*Trips - TOD %y%;~?q=1;y;0;(ms27*%r103%)+(ms28*%r104%); ; ;2
~+;1;y;ms34;y;tmp34;User Class 4 Trips - TOD %y%;~?q=1;y;0;ms27+ms28; ; ;2
~+;1;y;ms35;y;tmp35;User Class 4 Weighted VOT - TOD %y%;~?q=1;y;0;ms33/ms34; ; ;2
~+;1;y;ms36;y;tmp36;User Class 5 VOT*Trips - TOD %y%;~?q=1;y;0;(ms13*%r110%)+(ms14*%r109%); ; ;2
~+;1;y;ms37;y;tmp37;User Class 5 Trips - TOD %y%;~?q=1;y;0;ms13+ms14; ; ;2
~+;1;y;ms38;y;tmp38;User Class 5 Weighted VOT - TOD %y%;~?q=1;y;0;ms36/ms37; ; ;2
~# -- Final Toll Multipliers (Weight Factors) --
~+;1;y;ms84;y;tmp84;User Class 1 (SOV VOT1) toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r100%; ; ;2
~+;1;y;ms85;y;tmp85;User Class 2 (SOV VOT2) toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r101%; ; ;2
~+;1;y;ms86;y;tmp86;User Class 3 (SOV VOT3) toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r102%; ; ;2
~+;1;y;ms87;y;tmp87;User Class 4 (HOV) toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/ms35; ; ;2
~+;1;y;ms88;y;tmp88;b-plate + light truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/ms38; ; ;2
~+;1;y;ms89;y;tmp89;medium truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r111%; ; ;2
~+;1;y;ms90;y;tmp90;heavy truck toll multiplier (minutes/$) - TOD %y%;~?q=1;y;0;60/%r112%; ; ;2;q
~#
~#
~>>%t3%
~"Period %y% VOT Report:
~"
~"                                           Perceived VOT
~"                                         Relative to Tolls     Toll Multiplier
~"                            Trips             ($/hour)           (minutes/$)
~"SOV low VOT trips:       %ms24.2_>10%           %r100.2_>6%              %ms84.2_>5%
~"SOV medium VOT trips:    %ms25.2_>10%           %r101.2_>6%              %ms85.2_>5%
~"SOV high VOT trips:      %ms26.2_>10%           %r102.2_>6%              %ms86.2_>5%

~"HOV2 trips:              %ms27.2_>10%           %r103.2_>6%              %ms29.2_>5%
~"HOV3+ trips:             %ms28.2_>10%           %r104.2_>6%              %ms30.2_>5%
~"Overall HOV trips:       %ms34.2_>10%           %ms35.2_>6%              %ms87.2_>5%

~"B-Plate Truck trips:     %ms14.2_>10%           %r109.2_>6%              %ms31.2_>5%
~"Light Duty Truck trips:  %ms13.2_>10%           %r110.2_>6%              %ms32.2_>5%
~"Combined B-Plate+Light:  %ms37.2_>10%           %ms38.2_>6%              %ms88.2_>5%

~"Medium Duty Truck trips: %ms12.2_>10%           %r111.2_>6%              %ms89.2_>5%
~"Heavy Duty Truck trips:  %ms10.2_>10%           %r112.2_>6%              %ms90.2_>5%
~"External Truck trips:    %ms11.2_>10%           %r112.2_>6%              %ms90.2_>5%
~"------------------------------------------------------------------------------------------------------
~>>
~#
~/
~# ## DELETE REMAINING TEMPORARY MATRICES DURING PERIOD 8 ##
~+;~?y<8;~$>not_TOD8
3.12
~+;2;mf59;y;q
~:not_TOD8
~# 
~# ## -- Delete temp matrices ms10-ms38 --
~z=10
~:delms
~+;3.12;2;ms%z%;y;q
~z+1
~+;~?z<39;~$delms
~#
batchin=
reports=
~/
~/**********************************************************************
q
