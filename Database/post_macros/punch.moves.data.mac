~# punch.moves.data.mac
~#  Craig Heither, rev. 10-29-2018
~# 
~#  ****************************************************************************************
~#    Macro punches network results for MOVES emissions analysis - 6 vehicle class version.  
~#     - timau recalculated for vdf=7 using speed from incoming link.
~#     - toll collection facilities on ramps are flagged for later analysis.
~#     - @atype punched to identify urban vs. rural.
~#
~#    Must submit with 3-digit scenario number:
~#     (e.g. "~<post_macros\punch.moves.data.mac 400")
~#
~#      revised 10-11-2013: macro calls final_run_statistics.mac & run_vmt_statistics.mac
~#      NRF revised 2-25-2015: @avhov replaced with @avh2v and @avh3v for 7 vehicle class version
~#    Steve Chau edit (5/21/2015): Changed POE length (@mvlen) from 25 to 50
~#    Bozic 3-27-2017 Export bus veq on the network links along with other volume (@busveq) 
~#    Heither 10-29-2018: POE ranges updated for zone17 for long distance truck calculations (line 112)
~#    Bozic punch IM area on the network 11-15-2018
~#  *********************************************************************
~#  =====================================================================
~z=%0%
~+;~?!z=1;~$>error
~#
~o|39
~#
~## -- Set Up Register Values --
~t1=data\moves.data
~t2=report\moves.rpt
~t3=data\moves.longhaul.data
~#
~# -------------------------
~!if exist %t1% (del %t1% /Q)
batchout=%t1%
~!if exist %t2% (del %t2% /Q)
~!if exist %t3% (del %t3% /Q)
reports=%t2%
~#
~#  =====================================================================
~/  ## OBTAIN LINK DATA ##
~#
~## -- Set Register To Count Time Periods --
~x=1
~#
~:period
~## -- Copy Time Period Network To Temporary Scenario --
1.22
~+;2;99998;~?e; ;q;~$>next
~+;y;q
~:next
~+;1.22;3;%x%;99998; ;y;q
~#
~## -- Set Userfields And Punch --
2.41
~## -- Recalculate Timau For vdf=7 Using Speed From Incoming Link --
~## -- (flag links and use rate from incoming link [timau/len] for toll link - store timau values in ul3) --
~+;1;y;ui1;0; ;all;4
~+;1;y;ui2;0; ;all;4
~+;1;y;ul3;0; ;all;4
~+;1;y;ui1;vdf.eq.7; ;2;all;4
~+;1;y;uj2;(uj1.eq.1)*timau/len; ;2;all;4
~+;1;y;tmpl1;ui2*len; ;vdf=7; ;4
~+;1;y;ul3;tmpl1; ;vdf=7; ;4
~+;1;y;ul3;timau; ;!vdf=7; ;4
~#
~## -- Store Time Period In tmpl1 --
~+;1;y;tmpl1;0; ;all;4
~+;1;y;tmpl1;%x%; ;all;4
~#
~## -- Flag Toll Links on Ramps --
~## -- (based on incoming and outgoing links - store flag in tmpl2) --
~+;1;y;ui1;0; ;all;4
~+;1;y;ui2;0; ;all;4
~+;1;y;ui1;(vdf.eq.3 .or. vdf.eq.5 .or. vdf.eq.8); ;2;all;4
~+;1;y;uj2;(vdf.eq.3 .or. vdf.eq.5 .or. vdf.eq.8); ;2;all;4
~+;1;y;tmpl2;(ui2+uj1).ge.2; ;vdf=7; ;4
~#
~/ -- Punch Link Data: Period %x% --
~+;1;n;tmpl1+len+lan+vdf+@zone+@emcap+ul3+
@ftime+@avauv+@avh2v+@avh3v+@avbqv+@avlqv+
@avmqv+@avhqv+@atype+tmpl2+@busveq+@imarea
~+; ;all;3;q
~/
~## -- Delete Temporary Scenario --
~+;1.22;2;99998;y;q
~#
~## -- Iterate Through Time Periods --
~x+1
~+;~?x<9;~$period
~#
~#
~#  =====================================================================
~/  ## OBTAIN LONG DISTANCE (200+ MILES) M & H TRUCK VOLUME DATA ##
~#
batchout=%t3%
~#
~## -- Set Register To Count Time Periods --
~x=1
~#
~:longhaul
~#
~## -- Copy Time Period Network To Temporary Scenario --
~+;1.22;3;%1%4%x%;99998; ;y;q
~#
~## -- Create extra attributes --
2.42
~+;2;2;@m200;med truck long distance volau (MOVES);0
~+;2;2;@h200;hev truck long distance volau (MOVES);0
~+;2;2;@mvlen;length - truck long distance (MOVES);0;q
~#
~## -- Length attribute: set POE len to 25 to account for travel beyond network boundary --
2.41
~+;1;y;@mvlen;n;length; ;all;4
~+;1;y;@mvlen;n;50; ;i=3633,3649;j=3633,3649; ;4;q     / Changed POE length (@mvlen) from 25 to 50 (SC edit - 5/21/15)
~#
~## -- Path Analysis for m,h truck trips over 200 miles --
6.16
4                          / user defined path analysis
@mvlen                     / link attribute for path analysis
                           / no turn attribute
+                          / operator to compute path attributes
200.001,999999             / lower,upper thresholds
3                          / aggregation (average)
2                          / selected paths only
                           / no class 1 demand to be analyzed (default to assigned values)
                           / no matrix to hold class 1 O-D attribute
                           / no matrix to hold class 1 selected demand
                           / no extra attribute to hold class 1 link volumes
                           / no extra attribute to hold class 1 turn volumes
~+; ; ; ; ;                / skip all class 2 options
~+; ; ; ; ;                / skip all class 3 options
~+; ; ; ; ;                / skip all class 4 options
~+; ; ; ;@m200;n;          / save class 5 long distance link volumes (volau)
~+; ; ; ;@h200;n;          / save class 6 long distance link volumes (volau)
~+;2;q
~#
~## -- Set Userfields And Punch --
2.41
~## -- Store Time Period In tmpl1 --
~+;1;y;tmpl1;0; ;all;4
~+;1;y;tmpl1;%x%; ;all;4
~#
~/ -- Punch Longhaul Data: Period %x% --
~+;1;n;tmpl1+@m200+@h200; ;all;3;q
~/
~#
~## -- Delete Temporary Scenario --
~+;1.22;2;99998;y;q
~#
~## -- Iterate Through Time Periods --
~x+1
~+;~?x<9;~$longhaul
~#  =====================================================================
~#
~# ## -- Submit Run Statistics Macros -- ##
~<post_macros\final_run_statistics.mac %1%
~<post_macros\run_vmt_statistics.mac %1%
~#
~#
~$>end
~#
~:error
~/ +++++++++++++++++++++++++++++++++++++++
~/   SUBMIT WITH 3-DIGIT SCENARIO!!!!!!!
~/ +++++++++++++++++++++++++++++++++++++++
~/
~:end
~o=6
batchout=
reports=
~/ -- end of macro --
