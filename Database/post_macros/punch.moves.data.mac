~# punch.moves.data.mac
~#  Craig Heither, rev. 02-22-2023
~# 
~#  ****************************************************************************************
~#    Macro punches network results for MOVES emissions analysis - 7 vehicle class version.  
~#     - timau recalculated for vdf=7 using speed from incoming link.
~#     - toll collection facilities on ramps are flagged for later analysis.
~#     - @atype punched to identify urban vs. rural.
~#
~#    Must submit with 3-digit scenario number:
~#     (e.g. "~<post_macros\punch.moves.data.mac 400")
~#
~#      revised 10-11-2013: macro calls final_run_statistics.mac & run_vmt_statistics.mac
~#      NRF revised 2-25-2015: @avhov replaced with @avh2v and @avh3v for 7 vehicle class version
~#    Steve Chau edit (5/21/2015): Changed POE length (@mvlen) from 25 to 50
~#    Bozic 3-27-2017 Export bus veq on the network links along with other volume (@busveq) 
~#    Heither 10-29-2018: POE ranges updated for zone17 for long distance truck calculations (line 112)
~#    Bozic punch IM area on the network 11-15-2018
~#    Heither 07-09-2021, just punch timau r/t ul3 (net5i_7c.mac calculation was corrected)
~#    Heither 08-20-2021: punch separate link files for each time period, read Global Iteration value
~#							to automatically call appropriate scenario for long distance truck volumes,
~#							adjust path analysis for current user classes.
~#    Heither 11-11-2022: long distance truck analysis now completed in SOLA_assignment_final_global_iteration.py 
~#    Heither 02-22-2023: turn off engineering notation when exporting data to prevent issues reading the files. 
~#  *********************************************************************
~#  =====================================================================
~z=%0%
~+;~?!z=1;~$>error
~#
~o|39
~#
~## -- Set Up Register Values --
~t2=report\moves.rpt
~t3=data\moves.longhaul.data
~# -------------------------
~!if exist %t2% (del %t2% /Q)
~!if exist %t3% (del %t3% /Q)
reports=%t2%
~#
~## -- turn off engineering notation for export -- ##
off=22
~#
~#  =====================================================================
~/  ## OBTAIN LINK DATA ##
~#
~## -- Set Register To Count Time Periods --
~x=1
~#
~:period
~#
~## -- Write separate file for each time period --
~t1=data\moves_pd%x%.data
~!if exist %t1% (del %t1% /Q)
batchout=%t1%
~#
~## -- Copy Time Period Network To Temporary Scenario --
1.22
~+;2;99998;~?e
~+; ;q;~$>next
~+;y;q
~:next
~+;1.22;3;%x%;99998; ;y;q
~#
~## -- Set Userfields And Punch --
2.41
~## -- Store Time Period In tmpl1 --
~+;1;y;tmpl1;0; ;all;4
~+;1;y;tmpl1;%x%; ;all;4
~#
~## -- Flag Toll Links on Ramps --
~## -- (based on incoming and outgoing links - store flag in tmpl2) --
~+;1;y;ui1;0; ;all;4
~+;1;y;ui2;0; ;all;4
~+;1;y;ui1;(vdf.eq.3 .or. vdf.eq.5 .or. vdf.eq.8); ;2;all;4
~+;1;y;uj2;(vdf.eq.3 .or. vdf.eq.5 .or. vdf.eq.8); ;2;all;4
~+;1;y;tmpl2;(ui2+uj1).ge.2; ;vdf=7; ;4
~#
~/ -- Punch Link Data: Period %x% --
~+;1;n;tmpl1+len+lan+vdf+@zone+@emcap+timau+
@ftime+@avauv+@avh2v+@avh3v+@avbqv+@avlqv+
@avmqv+@avhqv+@atype+tmpl2+@busveq+@imarea+@speed
~+; ;all;3;q
~/
~## -- Delete Temporary Scenario --
~+;1.22;2;99998;y;q
~#
~## -- Iterate Through Time Periods --
~x+1
~+;~?x<9;~$period
~#
~#
~#  =====================================================================
~/  ## OBTAIN LONG DISTANCE (200+ MILES) M & H TRUCK VOLUME DATA ##
~#
batchout=%t3%
~#
~## -- Set Register To Count Time Periods --
~x=1
~#
~## -- Get Final Global Iteration Number to call appropriate scenario --
~z=%ms98%
~z-1
~#
~:longhaul
s=%1%%z%%x%
~#
~## -- Set Userfields And Punch --
2.41
~## -- Store Time Period In tmpl1 --
~+;1;y;tmpl1;0; ;all;4
~+;1;y;tmpl1;%x%; ;all;4
~#
~/ -- Punch Longhaul Data: Period %x% --
~+;1;n;tmpl1+@m200+@h200; ;all;3;q
~/
~#
~## -- Iterate Through Time Periods --
~x+1
~+;~?x<9;~$longhaul
~#  =====================================================================
~#
~# ## -- Submit Run Statistics Macros -- ##
~<post_macros\final_run_statisticsV2.mac %1%
~<post_macros\run_vmt_statistics.mac %1%
~#
~#
~$>end
~#
~:error
~/ +++++++++++++++++++++++++++++++++++++++
~/   SUBMIT WITH 3-DIGIT SCENARIO!!!!!!!
~/ +++++++++++++++++++++++++++++++++++++++
~/
~:end
~o=6
~## -- turn engineering notation back on -- ##
on=22
batchout=
reports=
~/ -- end of macro --
