      SUBROUTINE SUB_TRIPGEN7
      IMPLICIT INTEGER(A-Z)
C***********************************************************************
C        1         2         3         4         5         6         7
C23456789012345678901234567890123456789012345678901234567890123456789012
C***********************************************************************
C
C     THIS IS THE SEVENTH PROGRAM IN THE REVISED HOUSEHOLD TRIP
C     GENERATION PREPARED FOR THE PRAIRIE PARKWAY PROJECT.  THIS MODULE
C     ADJUSTS THE INITIAL TRIP PRODUCTIONS AND ATTRACTIONS TO ACCOUNT 
C     FOR TRIPS FROM OR TO EXTERNAL AREAS AND NONMOTORIZED TRAVEL.  THE
C     EXTERNAL TRIP FACTORS WERE DEVELOPED FROM THE CENSUS 2000 JOURNEY
C     TO WORK COUNTY FLOWS.  NONMOTORIZED TRIP FACTORS WERE COMPUTED
C     FROM THE 1995 NPTS AND SMOOTHED THROUGH REGRESSION.
C
C     EASH, SEPTEMBER 2003
C
C***********************************************************************
C
C     REVISED FOR I-290 HOV WORK IN NOVEMBER 2006.
C
C     1.  ACCOMODATES NEW TEMPORARY FILE FORMATS AND PUMS SUMMARIES.
C     2.  INCLUDES INPUT NONMOTORIZED FRACTIONS FOR WORK PRODUCTIONS 
C         AND ATTRACTIONS BY PUMA5 AREA.  THERE WERE ESTIMATED FROM
C         2000 CENSUS CTPP FILES.
C     3.  INCLUDES NEW INTERNAL-EXTERNAL FRACTIONS FOR PUMA5 
C         AREAS, ALSO DEVELOPED FROM 2000 CENSUS CTPP.  .
C
C     EXTERNAL TRIP PRODUCTIONS ARE TAKEN OUT FIRST.  FOR HOME-WORK 
C     TRIPS THE PROPORTION OF HOME-WORK EXTERNAL TRIP PRODUCTIONS IS 
C     DETERMINED BY THE PUMA5 "EXTERNAL" INPUT FILE.  AFTER TAKING OUT 
C     HOME-WORK PRODUCTIONS AN EQUAL NUMBER OF HOME-WORK ATTRACTIONS ARE 
C     ELIMINATED.  THE ATTRACTIONS ARE SUBTRACTED USING THE PUMA5 
C     ORIGIN-DESTINATION PROPORTIONS IN THE PUMA5 INPUT FILE.  NO 
C     EXTERNAL ADJUSTMENTS ARE ATTEMPTED FOR OTHER (SHORTER) TRIP 
C     PURPOSES.  THESE ADJUSTMENTS ARE THEN DISTRIBUTED PROPORTIONALLY
C     TO THE TAZS IN THE PUMA5.
C
C     NONMOTORIZED TRIP PRODUCTIONS ARE THEN FACTORED OUT.  FOR 
C     HOME-WORK TRIPS THE FRACTION OF NONMOTORIZED TRIPS BY PUMA5 IS
C     INPUT IN THE "NONMOTOR" INPUT FILE.  NOTE THAT THIS CALCULATIONS 
C     ONLY INVOLVES TRIPS THAT ARE INTERNAL IN THE STUDY AREA SINCE
C     EXTERNAL HOME-WORK TRIPS ARE PREVIOUSLY TAKEN OUT.  THE TRIP
C     REDUCTIONS ARE DISTRIBUTED AROUND THE TG ZONES IN THE HOME PUMA
C     USING THE 1995 NHTS RELATIONSHIPS.  
C
C     FOR ALL OTHER TRIP PURPOSES THE NONMOTORIZED TRIP REDUCTIONS ARE
C     BASED SOLELY ON THE 1995 NHTS RELATIONSHIPS.
C     
C     EASH, NOVEMBER 2006
C
C***********************************************************************
C
C     SUBROUTINE ROUTINE REVISED AGAIN TO TAKE INTO ACCOUNT DATA FROM
C     THE 2007-2008 CMAP HOUSEHOLD TRAVEL SURVEY.  EXTERNAL TRIP 
C     PROPORTIONS ADJUSTED FOR NEW STUDY AREA.
C
C     NONMOTORIZED TRIPS ARE NOW DEALT WITH IN A SEPARATE SUBROUTINE.
C     OTHER CHANGES NOTED IN BODY OF SUBROUTINE.
C
C     EASH FEBRUARY 2009
C
C***********************************************************************
      INCLUDE 'COMMON_EXT.FI'
	INCLUDE 'COMMON_PARAM.FI'
	INCLUDE 'COMMON_GEOG.FI'
	INCLUDE 'COMMON_ATTR.FI'
C
C     PUMA5 FACTORS FOR HOME-WORKPLACE EXTERNAL TRIP ADJUSTMENT
C     (ONLY FOR HOUSEHOLD GENERATED TRIPS
C
	REAL*4 P5_EXT_PFACT(100), P5_EXT_AFACT(100)

      REAL*4 EXTP, EXTA
	REAL*8  TOT_EXTP(2), TOT_EXTA(2), TEMPA
C
C     PUMA5 EXTERNAL TRIP FACTORS ARE INPUT ON UNIT 55.  
C
      OPEN (UNIT=55,FILE='EXT_IN.TXT',STATUS='OLD',ERR=955)
C
C     LOAD INDEPENDENT VARIABLE ARRAYS
C
	WRITE (*,'(/A/)') ' STARTING EXTERNAL TRIP ESTIMATES'
C
C     START EXTERNAL TRIP ADJUSTMENT FOR HOME-WORK TRIPS
C
   11 CONTINUE 
C
C     FIRST LOAD PUMA5 FACTOR ARRAYS
C
      READ (55,*,END=19) P5, EXTP, EXTA

      DO I=1,PUMA5
	  IF (P5 .EQ. P5_NUM(I)) THEN
	    P5_EXT_PFACT(I) = EXTP
	    P5_EXT_AFACT(I) = EXTA
        ENDIF
      ENDDO

	GO TO 11

   19 CONTINUE
C
C     WRITE OUT EXTERNAL FACTORS
C
	WRITE (16,'(/A)')  'EXTERNAL HOME WORKPLACE PROD-ATTR FACTORS'
	WRITE (16,'(A)')   '  PUMA5  PRODS  ATTRS'
      WRITE (16,'(A)')   '---------------------'

	DO I=1,PUMA5
	  IF (P5_NUM(I) .LT. 1) THEN
          WRITE (*,'(A)') ' MISSING PUMA5 IN EXTERNAL.TXT FILE' 
          STOP 19
        ENDIF 
C
	  WRITE (16,'(I7,2F7.3)')  P5_NUM(I), P5_EXT_PFACT(I), 
     A    P5_EXT_AFACT(I)
      ENDDO
C
C     FACTOR OUT EXTERNAL HOME-WORKPLACE PRODS AND ATTRS
C
      DO SZ=1,SUBZONES
        DO I=1,PUMA5
	    IF (SZ_PUMA5(SZ) .EQ. P5_NUM(I)) THEN
	      SZ_EXT_PRODS(SZ,1) = P5_EXT_PFACT(I) * SZ_PRODS(SZ,1)
	      SZ_EXT_PRODS(SZ,2) = P5_EXT_PFACT(I) * SZ_PRODS(SZ,2)
	      SZ_EXT_ATTRS(SZ,1) = P5_EXT_AFACT(I) * SZ_ATTRS(SZ,1)
	      SZ_EXT_ATTRS(SZ,2) = P5_EXT_AFACT(I) * SZ_ATTRS(SZ,2)
          ENDIF
        ENDDO
      ENDDO
C
C     BALANCE EXTERNAL ATTRACTIONS TO PRODUCTIONS
C
      DO SZ=1,SUBZONES
	  DO T=1,2
          TOT_EXTP(T) = TOT_EXTP(T) + SZ_EXT_PRODS(SZ,T)
          TOT_EXTA(T) = TOT_EXTA(T) + SZ_EXT_ATTRS(SZ,T)
        ENDDO
      ENDDO  
C
      DO SZ=1,SUBZONES
	  DO T=1,2
	    SZ_EXT_ATTRS(SZ,T) = 
     A      SZ_EXT_ATTRS(SZ,T)*TOT_EXTP(T)/TOT_EXTA(T)  
	  ENDDO  
      ENDDO
C
C     UPDATE PRODUCTIONS AND ATTRACTIONS AND ADJUST EXTERNAL 
C     ATTRACTIONS IF NECESSARY
C
      DO T=1,2
	  DO SZ=1,SUBZONES
		
		TEMPA = SZ_ATTRS(SZ,T) - SZ_EXT_ATTRS(SZ,T)
		IF (TEMPA .LT. 0.0) SZ_EXT_ATTRS(SZ,T) = SZ_ATTRS(SZ,T)
	 
          SZ_PRODS(SZ,T) = SZ_PRODS(SZ,T) - SZ_EXT_PRODS(SZ,T)
	    SZ_ATTRS(SZ,T) = SZ_ATTRS(SZ,T) - SZ_EXT_ATTRS(SZ,T)

        ENDDO
	ENDDO   
C
C     ACCUMULATE EXTERNAL TRIPS FOR SUMMARIES
C
      DO SZ=1,SUBZONES
	  DO J=1,COUNTIES
	    IF (SZ_CO(SZ) .EQ. CO_NUM(J)) ICO = J
	  ENDDO

        DO J=1,PUMA1
	    IF (SZ_PUMA1(SZ) .EQ. P1_NUM(J)) IP1 = J
	  ENDDO

        DO J=1,PUMA5
	    IF (SZ_PUMA5(SZ) .EQ. P5_NUM(J)) IP5 = J
	  ENDDO

	  ICHI = SZ_CHI(SZ)
	  ICBD = SZ_CBD(SZ)
C*****
C      WRITE (16,*) SZ, ICO, IP1, IP5
C*****
        DO T=1,2

	    CO_EXT_PRODS(ICO,T) = CO_EXT_PRODS(ICO,T) + SZ_EXT_PRODS(SZ,T)
	    CO_EXT_ATTRS(ICO,T) = CO_EXT_ATTRS(ICO,T) + SZ_EXT_ATTRS(SZ,T)
	    P1_EXT_PRODS(IP1,T) = P1_EXT_PRODS(IP1,T) + SZ_EXT_PRODS(SZ,T)
	    P1_EXT_ATTRS(IP1,T) = P1_EXT_ATTRS(IP1,T) + SZ_EXT_ATTRS(SZ,T)
	    P5_EXT_PRODS(IP5,T) = P5_EXT_PRODS(IP5,T) + SZ_EXT_PRODS(SZ,T)
	    P5_EXT_ATTRS(IP5,T) = P5_EXT_ATTRS(IP5,T) + SZ_EXT_ATTRS(SZ,T)

          REG_EXT_PRODS(T) = REG_EXT_PRODS(T) + SZ_EXT_PRODS(SZ,T)
          REG_EXT_ATTRS(T) = REG_EXT_ATTRS(T) + SZ_EXT_ATTRS(SZ,T)
C*****
C      WRITE (16,*) REG_EXT_PRODS(1), REG_EXT_PRODS(2)
C      WRITE (16,*) REG_EXT_ATTRS(1), REG_EXT_ATTRS(2)
C*****

          IF (ICHI .GT. 0) THEN 
            CHI_EXT_PRODS(T) = CHI_EXT_PRODS(T) + SZ_EXT_PRODS(SZ,T)
            CHI_EXT_ATTRS(T) = CHI_EXT_ATTRS(T) + SZ_EXT_ATTRS(SZ,T)
          ENDIF

          IF (ICBD .GT. 0) THEN 
            CBD_EXT_PRODS(T) = CBD_EXT_PRODS(T) + SZ_EXT_PRODS(SZ,T)
            CBD_EXT_ATTRS(T) = CBD_EXT_ATTRS(T) + SZ_EXT_ATTRS(SZ,T)
          ENDIF
        ENDDO
      ENDDO
C
      GO TO 999
C
  955 WRITE (*,'(A)') ' TROUBLE OPENING EXT_IN.TXT FILE' 
      STOP 955
  956 WRITE (*,'(A)') ' TROUBLE OPENING EXT_PA_OUT.TXT FILE' 
      STOP 956
  957 WRITE (*,'(A)') ' TROUBLE OPENING SECOND_PA_OUT.TXT FILE' 
      STOP 957
C
  999 CONTINUE
C
C     PRINT ROW AND COLUMN ATTRACTIONS FOR CHECKING
C
	WRITE(16,'(/A)') 'EXTERNAL TRIP PRODUCTION AND ATTRACTION SUMMARIE
     AS'
	WRITE (16,'(/A)') 'COUNTIES'
	WRITE (16,'(/A)') 'HOME-WORKPLACE EXTERNAL TRIPS'
	WRITE (16,'(A)')  '          PRODUCTIONS     ATTRACTIONS '
      WRITE (16,'(A)')  '        --------------- ---------------'
	WRITE (16,'(A)')  ' COUNTY  <$ INC  >$ INC  <$ INC  >$ INC'  
	WRITE (16,'(A)')  '---------------------------------------'       

	DO I=1,COUNTIES
	  WRITE (16,'(I7,4F8.0)')  CO_NUM(I), 
     A    (CO_EXT_PRODS(I,T),T=1,2), (CO_EXT_ATTRS(I,T),T=1,2) 
     	ENDDO

	WRITE (16,'(A,4F8.0)')  '  TOTAL',
     A  (REG_EXT_PRODS(T),T=1,2), (REG_EXT_ATTRS(T),T=1,2) 
	WRITE (16,'(/A,4F8.0)') 'CHICAGO', 
     A  (CHI_EXT_PRODS(T),T=1,2), (CHI_EXT_ATTRS(T),T=1,2) 
	WRITE (16,'(A,4F8.0)')  'CHI CBD', 
     A  (CBD_EXT_PRODS(T),T=1,2), (CBD_EXT_ATTRS(T),T=1,2) 
C
C     REPEAT FOR ONE PERCENT PUMS
C
	WRITE (16,'(/A)') 'ONE PERCENT PUMAS'
	WRITE (16,'(/A)') 'HOME-WORKPLACE EXTERNAL TRIPS'
	WRITE (16,'(A)')  '          PRODUCTIONS     ATTRACTIONS '
      WRITE (16,'(A)')  '        --------------- ---------------'
	WRITE (16,'(A)')  '  PUMA1  <$ INC  >$ INC  <$ INC  >$ INC'  
	WRITE (16,'(A)')  '---------------------------------------'       

	DO I=1,PUMA1
	  WRITE (16,'(I7,4F8.0)')  P1_NUM(I), 
     A    (P1_EXT_PRODS(I,T),T=1,2), (P1_EXT_ATTRS(I,T),T=1,2) 
     	ENDDO

	WRITE (16,'(A,4F8.0)')  '  TOTAL',
     A  (REG_EXT_PRODS(T),T=1,2), (REG_EXT_ATTRS(T),T=1,2) 
C
C     REPEAT FOR FIVE PERCENT PUMS
C
	WRITE (16,'(/A)') 'FIVE PERCENT PUMAS'
	WRITE (16,'(/A)') 'HOME-WORKPLACE EXTERNAL TRIPS'
	WRITE (16,'(A)')  '          PRODUCTIONS     ATTRACTIONS '
      WRITE (16,'(A)')  '        --------------- ---------------'
	WRITE (16,'(A)')  '  PUMA5  <$ INC  >$ INC  <$ INC  >$ INC'  
	WRITE (16,'(A)')  '---------------------------------------'       

	DO I=1,PUMA5
	  WRITE (16,'(I7,4F8.0)')  P5_NUM(I), 
     A    (P5_EXT_PRODS(I,T),T=1,2), (P5_EXT_ATTRS(I,T),T=1,2) 
     	ENDDO

	WRITE (16,'(A,4F8.0)')  '  TOTAL',
     A  (REG_EXT_PRODS(T),T=1,2), (REG_EXT_ATTRS(T),T=1,2) 
C
C     OPTION TO OUTPUT FORMATED FILE OF PRODUCTIONS AND ATTRACTIONS
C
      IF (SAVE_FILE) THEN
C
C     FILE OF EXTERNAL PRODUCTIONS AND ATTRACTIONS IN TWO CATEGORIES
C     IS UNIT 56  
C
        OPEN (UNIT=56,FILE='EXTERNAL_PA_OUT.TXT',STATUS='NEW',ERR=956)
        OPEN (UNIT=57,FILE='SECOND_PA_OUT.TXT',STATUS='NEW',ERR=957)

	  DO T=1,2
	    DO SZ = 1,SUBZONES
            WRITE (56,'(2I6,I2,4F9.1)')  SZ, SZ_ZONE(SZ), T, 
     A        SZ_EXT_PRODS(SZ,T), SZ_EXT_ATTRS(SZ,T)
	    ENDDO
        ENDDO
        CLOSE (56,DISP='KEEP')
C
	  DO T=1,49
	    DO SZ = 1,SUBZONES
	      WRITE (57,'(2I6,I2,2F9.1)')  SZ, SZ_ZONE(SZ), T, 
     A        SZ_PRODS(SZ,T), SZ_ATTRS(SZ,T)
	    ENDDO
        ENDDO
	  CLOSE (57,DISP='KEEP')
      ENDIF
C
	CLOSE (55,DISP='KEEP')

      WRITE (16,'(/A)') ' END OF TRIPGEN7'
      WRITE (*,'(/A)') ' END OF TRIPGEN7'
      
	RETURN
      END